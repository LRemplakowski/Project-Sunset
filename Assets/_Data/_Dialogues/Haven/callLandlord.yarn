title: CallLandlord
tags:
position: -328,-104
---
<<declare $kevinRollModifier to 0>>
[nomarkup][You pick up the receiver.][/nomarkup]
-> [nomarkup][Call the police.][/nomarkup] <<if $police == true>>
    {$beast}: To change one cage for another? Not the smartest thing to do.
    [nomarkup][A strange whisper in your head compels you to stop, and surprisingly, you can't disagree with it. Your conscience won't win tonight. You're not going to jail.][/nomarkup]
    <<set $police to false>>
-> [nomarkup][Call the landlord.][/nomarkup]
    {$landlord}: Sunset Acres Apartments, how can I help you?
    {$pcname}: Hey Kevin, it's Desiree. Sink is clogged again.
    {$landlord}: [nomarkup][You can hear his contempt through the phone.][/nomarkup] Again? There's no way. I just fixed it last week. You must be washing grease down the drain. Not my problem. Hire a plumber.
    -> {$pcname}: I'll pay you for your time.
        [nomarkup][Now you're speaking his language.][/nomarkup]
        {$landlord}: I'll have to charge you a... uh... a late night fee. $200.
        -> {$pcname}: [nomarkup][You grit your teeth.][/nomarkup] Sure.
            [nomarkup][He hangs up.][/nomarkup]
            <<CompleteObjective ("FIX_SINK","CALL_LANDLORD")>>   
            <<stop>>
        -> {$pcname}: You're trying to rip me off!
            {$landlord}: I'm a capitalist. Take it or leave it. I'm going back to bed.
            {$pcname}: Fine. I'll take it.
            -> [nomarkup][Hang up.][/nomarkup]
                <<CompleteObjective ("FIX_SINK","CALL_LANDLORD")>>   
                <<stop>>
    -> {$pcname}: (Manipulation + Persuasion) Please, Kevin? I'm just trying to get my life together here. <<if visited("ManipPersKevin") == false>>
        <<jump ManipPersKevin>>
    -> {$pcname}: (Charisma + Subterfuge) [nomarkup][You spend several moments gathering up the willpower to make your voice sound more appealing - or the closest thing to what you assume is appealing to this man that you can muster.][/nomarkup] What if I make it worth your while? <<if visited("ChaSubtKevin") == false>>
        <<jump ChaSubtKevin>>
    -> {$pcname}: (Manipulation + Intimidation) Kevin, you know I'm friends with tenant lawyers right? <<if visited("ManipIntimKevin") == false>>
        <<jump ManipIntimKevin>>
-> [nomarkup][Hang up and walk away.][/nomarkup]
    <<stop>>
===
title: ContinueCallLandlord
tags:
position: 408,-214
---
{$landlord}: So, anything else you have to offer?
-> {$pcname}: I'll pay you for your time.
    [nomarkup][Now you're speaking his language.][/nomarkup]
    {$landlord}: I'll have to charge you a... uh... a late night fee. $200.
    -> {$pcname}: [nomarkup][You grit your teeth.][/nomarkup] Sure.
        [nomarkup][He hangs up.][/nomarkup]
        <<CompleteObjective ("FIX_SINK","CALL_LANDLORD")>>   
        <<stop>>
    -> {$pcname}: You're trying to rip me off!
        {$landlord}: I'm a capitalist. Take it or leave it. I'm going back to bed.
        {$pcname}: Fine. I'll take it.
        -> [nomarkup][Hang up.][/nomarkup]
            <<CompleteObjective ("FIX_SINK","CALL_LANDLORD")>>   
            <<stop>>
-> (Manipulation + Persuasion) {$pcname}: Please, Kevin? I'm just trying to get my life together here. <<if visited("ManipPersKevin") == false>>
    <<jump ManipPersKevin>>
-> (Charisma + Subterfuge) {$pcname}: [nomarkup][You spend several moments gathering up the willpower to make your voice sound more appealing - or the closest thing to what you assume is appealing to this man that you can muster.][/nomarkup] What if I make it worth your while? <<if visited("ChaSubtKevin") == false>>
    <<jump ChaSubtKevin>>
-> (Manipulation + Intimidation) {$pcname}: Kevin, you know I'm friends with tenant lawyers right? <<if visited("ManipIntimKevin") == false>>
    <<jump ManipIntimKevin>>
===     
title: ManipPersKevin
tags:
position: 113,-107
---            
<<if Roll("Manipulation","Persuasion") >= (3 + $kevinRollModifier)>>
    {$landlord}: It must be hard for you, not having a man in your life. Wearing the pants is more difficult than you'd expect, huh? Guess I'll have to help out - show you the ropes. #success
    [nomarkup][In response, you make an expression that is equal parts revolted, frustrated, and perturbed. Why does the world have to go out of its way to remind you that people with opinions like this still exist? You swallow the emotion with some effort.][/nomarkup]
    {$pcname}: Yeah, it's been difficult. But you don't want to hear about the woes of a poor, depressed divorcee...
    {$landlord}: Ah, but I've said I would teach you, didn't I? Just let me get dressed. Think you can hold it together until I get there?
    {$pcname}: Sure. Please, don't keep me waiting. [nomarkup][You say this through teeth gritted in a smile that doesn't reach your eyes. It's a saccharine-sweet, enthusiastic tone - your customer service voice.][/nomarkup]  
    [nomarkup][He hangs up.][/nomarkup]
    <<CompleteObjective ("FIX_SINK","CALL_LANDLORD")>>   
    <<stop>>
<<else>>
    {$landlord}: Don't act like we're friends. You don't even know my last name. I only let you sign the lease because I pitied you. I guess I'll extend my pity one last time, but after that, you're on your own. #failure
    <<set $kevinRollModifier to inc($kevinRollModifier)>>
    {$pcname}: [nomarkup][What an asshole.][/nomarkup]
    <<jump ContinueCallLandlord>>
<<endif>>  
===
title: ChaSubtKevin
tags:
position: 157,51
---
<<if Roll("Charisma","Subterfuge") >= (3 + $kevinRollModifier)>>
    {$landlord}: Oh... anything you say? #failure
    {$pcname}: Anything.
    {$pcname}: [nomarkup][Ew! I can't believe I'm doing this...][/nomarkup]
    [nomarkup][You hold back a gag.][/nomarkup]
    {$landlord}: I'll be right over. [nomarkup][You can hear the smug grin in his voice. Disgusting.][/nomarkup]
    {$pcname}: Okay.
    [nomarkup][He hangs up.][/nomarkup]
    <<CompleteObjective ("FIX_SINK","CALL_LANDLORD")>>   
    <<stop>>
<<else>>
    {$landlord}: Nice try, dear. I know I'm a catch, but I don't stick my dick in divorcees. Too much baggage. I guess I'll help just because you don't have any other man in your life, but keep your hands to yourself. Frankly, I'm out of your league. #failure
    <<set $kevinRollModifier to inc($kevinRollModifier)>>
    {$pcname}: [nomarkup][What an asshole, “Out of your league.” Really? You wonder how many more delusions about himself and how desirable he is are swirling about in that canister of hot air he calls a brain...][/nomarkup]
    <<jump ContinueCallLandlord>>
<<endif>> 
===
title: ManipIntimKevin
tags:
position: 85,-289
---
<<if Roll("Manipulation","Intimidation") >= (3 + $kevinRollModifier)>>
    {$landlord}: Oh, I didn't know that. I'll um... I'll be right over. #success
    [nomarkup][Well, that seemed to shut him up. You just hope he never actually gets around to looking into your claim...][/nomarkup]
    {$pcname}: Now you do.
    -> [nomarkup][Hang up.][/nomarkup]
        <<CompleteObjective ("FIX_SINK","CALL_LANDLORD")>>   
        <<stop>>
<<else>>
    {$landlord}: [nomarkup][He snorts out a laugh on the other end.][/nomarkup] You're trying to intimidate me? After I put a roof over your head? Get the fuck out of here with your empty threats. #failure
    {$pcname}: [nomarkup][And I'm paying through the nose for that same roof.][/nomarkup] You done?
    {$landlord}: Yeah.
    <<set $kevinRollModifier to inc($kevinRollModifier)>>
    <<jump ContinueCallLandlord>>
<<endif>>
===

