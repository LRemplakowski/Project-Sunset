title: meetingKate
position: -178,-136
---
[nomarkup][The woman before you looks like she is in her mid-40s, but beautiful despite beginning to show her age on her face. She’s wearing a corset heavily embroidered with gold - tied so tight you’re convinced her organs are in her throat. Its pattern shows proud peacocks ostentatiously presenting their tail feathers. Her dark red hair cascades downwards towards that cinched waistline, the same hue as the short dress she is wearing. Decorated with sequins, said garment shimmers slightly, reflecting the light around her. And out of that flared skirt poke slender and pale legs, adorned with a pair of lace stockings.][/nomarkup]
[nomarkup][Following a simple once-over, your eyes are drawn to her crimson red nails encircling the brim of a glass containing a dark red liquid that may pass for wine, but you get a feeling it’s something far more sinister… The soft red light that illuminates her heavy, but comfortable armchair is dim but ominous. This is the perch of a ruler, the throne which declares her the red queen of this place…][/nomarkup]
{$innerVoice}: This waistline… geez… good thing she doesn’t need to breathe…
<<if GetIsPartyMemberRecruited("ANASTASIA") is false>>
    {$sforza}:Kieran. Back so soon? Hope Dominic didn’t run you off. Who’s your friend? [nomarkup][Her eyes are turned on you. She speaks with a strange manner and accent you can’t quite place. Something European, perhaps?][/nomarkup]
<<else>>
    {$sforza}: Kieran, back so soon? Hope Dominic didn’t run you off. Who are your friends? [nomarkup][Her eyes are turned on you. She speaks with a strange manner and accent you can’t quite place. Something European, perhaps?][/nomarkup]
    {$anastasia}: I am Anastasia, your High Holiness Miss Majesty. [nomarkup][She sinks into a deep curtsy - clearly mocking, much to Kieran’s dismay.][/nomarkup]
    {$sforza}: [nomarkup][Deadpan][/nomarkup] Adorable. What a surprise and a pleasure for you to finally do me the courtesy of introducing yourself! And what a delight you turned out to be my dear, I cannot imagine why we didn’t get together sooner. Were you afraid that I’d bite? [nomarkup][Kate’s smile doesn’t reach her eyes, but she allows this particular insult to go unpunished.][/nomarkup]
<<endif>>
[nomarkup][Kieran’s demeanor noticeably changes around Kate; he’s more skittish and docile than his usual self, looking up at Kate with a dose of admiration akin to a believer beholding his goddess. On some level, it appears absurd, given what you know of the man, and you debate whether or not you should avert your eyes.][/nomarkup]
{$kieran}: [nomarkup][He clears his throat upon noticing your gaze. He’s clearly embarrassed.][/nomarkup] Good evening Kate. You surely remember that Dominic decided to take me with him on one of his “missions”. This is the fledgling we were tasked with apprehending.
{$sforza}: [nomarkup][Incredulous, she lifts an eyebrow.][/nomarkup] I see Dominic is trying to start another Court scandal. How nice of him. Does Prince Douglas know?
{$kieran}: [nomarkup][He chuckles nervously.][/nomarkup] I’ll leave their marital drama up to them. But she probably knows. And if not now, then she will soon. As for your guest… [nomarkup][He turns to you.][/nomarkup] Meet miss Desiree Bardot. She’s a PI. Dominic is of the opinion that she has enough potential for a simple execution to be a waste. Her first task is to find her Sire to minimize the scandal.
[nomarkup][This is not how events unfolded in your apartment. It was Kieran who needed to convince Dominic of her competence, not the other way around. Moreover, Kieran’s omission of your deadline is… glaring, to say the least. You feel a heavy iron knot tightening in your gut. How bad is the current situation if Kieran feels the need to hide the truth from even his Sire?][/nomarkup]
{$beast}: So fond of her. And still able to lie in her face to cover his ass.
{$sforza}: Interesting. [nomarkup][She gives you an ocular pat down, eyeing the length of your figure, and smiles this time a smile that reaches her eyes.][/nomarkup] I suppose if Dominic believes you’re worth breaking the Traditions over, then you must be something special?. Good luck with this little test my dear, come back if you succeed? And tell me all about it, I’m so curious!
-> {$pcName}: Thanks.
    {$sforza}: At least you know your manners. That helps. [nomarkup][+1 Kieran Influence][/nomarkup]
    <<ModifyInfluence 1 $kieran>>
    <<jump questionsAboutLastNight>>
-> {$pcName}: How generous of you. [nomarkup][You say with a dose of sarcasm.][/nomarkup]
    {$sforza}: [nomarkup][She laughs, yet her tone is far from amused.][/nomarkup] My dear, you should learn to be more guarded with your intentions if you want to make something of your new life. Not all Kindred are as nice as I am, some think that the solution to a wounded pride is a missing tooth.. [nomarkup][-1 Kieran Influence][/nomarkup]
    <<ModifyInfluence -1 $kieran>>
    {$kieran}: She does have a fiery temperament, but… it’s to be expected, given her Blood.
    {$sforza}: Brujah? [nomarkup][She sighs and rolls her eyes.][/nomarkup] I would heartily endorse some anger management classes and, if you would like to live a long prosperous life, to not lean on excuses.
    <<jump questionsAboutLastNight>>
===
title: questionsAboutLastNight
position: 6,20
---
{$pcName}: I came by here last night. Do you remember me? Who I was with?
{$sforza}: I see a lot of faces my dear, this is a popular club! But- I can see that this is serious allow me to recall… [nomarkup][Her lips curl in an amused grin as her eyes drift off to the side, her focus turning towards her own memories.][/nomarkup] Ah, yes I do remember someone mentioning a woman of your description. You’re quite famous around these parts, I remember saying that if you were to stop by your first drink would be on me. Thank you for that article on the Golden Lion my dear, it has brought me no end of amusement. I was told you found yourself something of a partner in crime and left before anyone had the opportunity. [nomarkup][She makes a wide, sweeping gesture with her hand, chuckling.][/nomarkup] Maybe he’s your Sire?
{$innerVoice}: \[Your face flushes red. Apparently even vampires can be embarrassed. It's mixed with guilt.\]
{$kieran}: Maybe, but I think that's most likely the guy we found in her apartment. What was left of him anyway.
{$sforza}: Making a mess already, piccola mia. Hopefully Kieran can teach you how to behave. I like to think I taught him well.
[nomarkup][Kieran only reciprocates with an anxious nod.][/nomarkup]
{$pcName}: Did we leave with anybody else?
{$sforza}: Not that I saw, but I couldn't give you an honest answer. I was preoccupied.
-> {$pcName}: (Insight) “Preoccupied?”
    <<if Roll("wits","insight")>=1>>
        {$sforza}: If you must know, Diana arranged an evening with one of my favorite “playthings.” I'll leave the rest to your imagination. #success
        <<jump kateFarewell>>
    <<else>>
        {$sforza}: \[Smirks\] A lady shouldn't kiss and tell. #failure
        <<jump kateFarewell>>
    <<endif>>
===
title: kateFarewell
position: 293,-24
---
<<if GetIsPartyMemberRecruited("ANASTASIA") is false>>
{$sforza}: Before you go, I may have a lead for you. See the metalhead bimbo down there?
    <<if visited("anastasiaMeeting")==true>>
        [nomarkup][Kate points to Anastasia. Even from this distance, you can tell she's still trying to flirt with the bartender, and by the looks of it, it's not going well.][/nomarkup]
        <<jump kateNudgesTowardAnastasia>>
    <<else>>
        [nomarkup][Kate points to a woman decked out in studded leather garments and cleats. Even from this distance, you can tell she's blatantly trying to flirt with the bartender, and by the looks of it, it's not going well.][/nomarkup]
        <<jump kateNudgesTowardAnastasia>>
    <<endif>>
<<else>>
-> {$pcName}: Thank you for your time.
    {$sforza}: I'd say “anytime” dear, but that wouldn't be honest. My time is precious. I do wish you best on your endeavor however. You might even be of some use to me down the line.
<<endif>>
===
title: kateNudgesTowardAnastasia
position: 308,190
---
{$sforza}: Name's Anastasia. She's one of us. Shown up the past few nights, but hasn't done the courtesy of introducing herself. I've been keeping an eye on her. Hasn't caused any problems to my knowledge, but she might be your gal.

{$kieran}: You're tolerating poachers in your domain?

{$sforza}: She hasn't poached yet just lurking. I've been sniffing her out. Think she's staking out the place, or she's helplessly in love with my staff. Either way, if you saved me the hassle of dealing, I'd be grateful.
===
title: subsequentInteractions
---
<<if GetIsPartyMemberRecruited($anastasia) is false>>
    {$sforza}: Back again, piccola mia? I suppose I have a few minutes.
<<else>>
    <<if $anastasiaIntroduced ==false>>
        {$sforza}: Pleasure to finally make your acquaintance, Anastasia.
        
        [nomarkup][Anastasia raises an eyebrow, but keeps her bearings.][/nomarkup]
        
        {$anastasia}: Likewise, Caterina.
        
        [nomarkup][She kisses Kate's hand. Kate wipes it off on the table.][/nomarkup]
    <<endif>>
<<endif>>
{$sforza}: Let's cut to the chase, what do you want?
-> {$pcName}: What can you tell me about Diana?
    <<jump whoIsDiana>>
-> {$pcName}: How did you come to own this place?
    <<jump howSheStarted>>
-> {$pcName}: Tell me about youself. <<if visited("kateTalksAboutHerself") == false>>
    <<jump kateTalksAboutHerself>>
<<if $dianaMet ==true>>
-> {$pcName}: I need to get into Diana's office.
<<endif>>
-> {$pcName}: I should go.
    {$sforza}: Best of luck on your little search.
===
title: whoIsDiana
---
{$sforza}: What about her? She's my ghoul. She owns this place on paper. Runs the bar to keep up appearances, makes the place feel authentic. She's been with me for some time now. Why do you ask?
-> {$pcName}: You mentioned she was here last night.
    <<jump dianaEveryNight>>
<<if $dianaMet ==true>>
    -> {$pcName}: She led me into the alley last night.
<<endif>>
    <<jump whatDianaDid>>
===
title: dianaEveryNight
---
    {$sforza}: Hardly news. She's here almost every night.
    -> {$pcName}: Almost every night?
    {$sforza}: I may be old, but I'm not blind. She's been seeing someone on occasion. It hasn't interfered with her duties, so I haven't said anything. I like her having her own life. Keeps things interesting.
    <<if GetIsPartyMemberRecruited("ANASTASIA") is true>>
        {$anastasia}: \[Scoffs\] How generous of you.
    <<endif>>
    <<jump whoIsDiana>>
===
title: whatDianaDid
---
{$sforza}: You should talk to her about that then.
-> {$pcName}: She ran to her office before I had the chance.
    [nomarkup][Kate cocks her head to the side.][/nomarkup]
    
    {$sforza}: Curious. She's probably just busy.
    <<set $kateAlleyTalk to true>>
    <<jump subsequentInteractions>>
<<if $kateAlleyTalk ==true>>
    {$sforza}: Talk to the bouncer at the door to her office. Tell him I sent you.
<<endif>>
<<if $kateSentUs ==true>>
    {$kieran}: We appreciate your assistance in this manner.
<<else>>
    -> {$pcName}: The bouncer is being difficult.
        <<jump bouncerDifficulties>>
<<endif>>
===
title: howSheStarted
---
{$sforza}: Diana owns the place, piccola mia. I just look after it. It was a gift from our “beloved”... 

[nomarkup][She chews on that word.][/nomarkup]
<<if GetIsPartyMemberRecruited("ANASTASIA") is true>>
    
    {$anastasia}: \[Whispering\] I believe her.
<<endif>>
<<jump additionalQuestions>>
===
title: additionalQuestions
---
{$sforza}: ... Prince. She saw fit to bestow upon me this modest domain as a token of her gratitude after that nasty business with what happened to the previous Prince. I knew that The Embassy would be the perfect spot to establish a place where Kindred could gather and socialize, a true haven for our dysfunctional little community. It required a bit of remodeling and adapting to the needs of our kind, but with Diana's help and expertise, it has become one of the most popular spots for us in the city.
    -> {$pcName}: What do you think about the Prince?
        <<jump katePrinceOpinion>>
    -> {$pcName}: Who is a Prince? This title seems to repeat in nearly all discussions with... our kind.
        <<jump princeExplained>>
    -> {$pcName}: What happened to the previous Prince?
        <<jump previousPrince>>
===
title: katePrinceOpinion
---
[nomarkup][Kieran glares at you.][/nomarkup]
{$sforza}: It is my duty to follow her lead and contribute to the betterment of our community.
-> {$pcName}: That's a non-answer.
    {$sforza}: But it is the only answer you'll get.
    {$kieran}: We have an investigation to get back to.
    <<jump additionalQuestions>>
===
title: princeExplained
---
{$sforza}: Ah, it's so refreshing to have someone as oblivious to politics as you. But frankly, I don't have time to teach you everything you need to know.

{$kieran}: The Prince is the head figure in charge of the Camarilla's capital-D Domain here in Las Vegas - nearly the entire valley aside from Henderson and Sunset Manor is under our control. I'll tell you more when we've finished this investigation of ours.
<<jump additionalQuestions>>
===
title: previousPrince
---
{$sforza}: Benedic was the Prince before Sam. Bastard got himself killed by Sabbat assassins, but they learned their lesson after we hunted them down.
-> {$pcName}: Sabbat?
    <<jump whatIsSabbat>>
-> {$pcName}: Traditions?
    <<jump whatAreTraditions>>
-> {$pcName}: Camarilla?
    <<jump camarillaExplained>>
===
title: whatIsSabbat
---
    {$kieran}: A bunch of savages.
    
    [nomarkup][Kates gestures towards Kieran in approval.][/nomarkup]
    
    {$sforza}: A dangerous, misguided organization that rejects the Traditions and laws that keep our kind from becoming animals. Benedic didn't see them as a serious threat and now you know what happened to him. Someone in your position, the less you know about them. The better.
    <<if GetIsPartyMemberRecruited("ANASTASIA") is true>>
        {$anastasia}: It's just the natural order of things. \[she shrugged\]
    <<endif>>
    <<jump previousPrince>>
===
title: whatAreTraditions
---
{$kieran}: A set of rules that everyone has to adhere to survive. Something like the Ten Commandments, if you're religious.
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: More like Hammurabi's Code, if we're talking severity of retribution. Death usually isn't on the table when talking about Christianity.
    [nomarkup][Kieran sighs][/nomarkup]
    {$anastasia}: What? You know I'm right. Forgiveness, and all that junk.
<<endif>>
<<jump additionalQuestions>>
===
title: camarillaExplained
---
{$kieran}: That's us. The good guys, so to speak.
<<jump additionalQuestions>>
===
title: kateTalksAboutHerself
---
[nomarkup][Kate just laughs. Kieran only looks down at the ground and shakes his head side to side.][/nomarkup]
{$sforza}: Don't you have an investigation to get back to?
<<jump subsequentInteractions>>
===
title: bouncerDifficulties
---
{$sforza}: After you told him I sent you?
-> {$pcName}: Yes ma'am.
    {$sforza}: That won't do at all. Stay here. I'll deal with this myself.
    <<set $kateDealsWithBouncer to true>>
    //cut to black//
    {$sforza}: Apologies for the delay. He won't be an issue anymore.
    {$kieran}: Thank you ma'am.
    -> {$pcName}: What did you do to him?
        {$kieran}: Pro tip. Best not to ask questions.
        <<if GetIsPartyMemberRecruited($anastasia) is true>>
            {$anastasia}: Oooooh how ominous.
            <<jump subsequentInteractions>>
        <<endif>>
===