title: meetingKate
position: -178,-136
---
[nomarkup][The woman before you looks like she is in her mid-40s, but beautiful despite beginning to show her age on her face. She's wearing a corset heavily embroidered with gold - tied so tight you're convinced her organs are in her throat. Its pattern shows proud peacocks ostentatiously presenting their tail feathers. Her dark red hair cascades downwards towards that cinched waistline, the same hue as the short dress she is wearing. Decorated with sequins, said garment shimmers slightly, reflecting the light around her. And out of that flared skirt poke slender and pale legs, adorned with a pair of lace stockings.][/nomarkup]
[nomarkup][Following a simple once-over, your eyes are drawn to her crimson red nails encircling the brim of a glass containing a dark red liquid that may pass for wine, but you get a feeling it's something far more sinister... The soft red light that illuminates her heavy, but comfortable armchair is dim but ominous. This is the perch of a ruler, the throne which declares her the red queen of this place...][/nomarkup]
{$innerVoice}: This waistline... geez... good thing she doesn't need to breathe...
<<if GetIsPartyMemberRecruited("ANASTASIA") is false>>
    {$sforza}:Kieran. Back so soon? Hope Dominic didn't run you off. Who's your friend? [nomarkup][Her eyes are turned on you. She speaks with a strange manner and accent you can't quite place. Something European, perhaps?][/nomarkup]
<<else>>
    {$sforza}: Kieran, back so soon? Hope Dominic didn't run you off. Who are your friends? [nomarkup][Her eyes are turned on you. She speaks with a strange manner and accent you can't quite place. Something European, perhaps?][/nomarkup]
    {$anastasia}: I am Anastasia, your High Holiness Miss Majesty. [nomarkup][She sinks into a deep curtsy - clearly mocking, much to Kieran's dismay.][/nomarkup]
    {$sforza}: [nomarkup][Deadpan][/nomarkup] Adorable. What a surprise and a pleasure for you to finally do me the courtesy of introducing yourself! And what a delight you turned out to be my dear, I cannot imagine why we didn't get together sooner. Were you afraid that I'd bite? [nomarkup][Kate's smile doesn't reach her eyes, but she allows this particular insult to go unpunished.][/nomarkup]
<<endif>>
[nomarkup][Kieran's demeanor noticeably changes around Kate; he's more skittish and docile than his usual self, looking up at Kate with a dose of admiration akin to a believer beholding his goddess. On some level, it appears absurd, given what you know of the man, and you debate whether or not you should avert your eyes.][/nomarkup]
{$kieran}: [nomarkup][He clears his throat upon noticing your gaze. He's clearly embarrassed.][/nomarkup] Good evening Kate. You surely remember that Dominic decided to take me with him on one of his “missions”. This is the fledgling we were tasked with apprehending.
{$sforza}: [nomarkup][Incredulous, she lifts an eyebrow.][/nomarkup] I see Dominic is trying to start another Court scandal. How nice of him. Does Prince Douglas know?
{$kieran}: [nomarkup][He chuckles nervously.][/nomarkup] I'll leave their marital drama up to them. But she probably knows. And if not now, then she will soon. As for your guest... [nomarkup][He turns to you.][/nomarkup] Meet miss Desiree Bardot. She's a PI. Dominic is of the opinion that she has enough potential for a simple execution to be a waste. Her first task is to find her Sire to minimize the scandal.
[nomarkup][This is not how events unfolded in your apartment. It was Kieran who needed to convince Dominic of her competence, not the other way around. Moreover, Kieran's omission of your deadline is... glaring, to say the least. You feel a heavy iron knot tightening in your gut. How bad is the current situation if Kieran feels the need to hide the truth from even his Sire?][/nomarkup]
{$beast}: So fond of her. And still able to lie in her face to cover his ass.
{$sforza}: Interesting. [nomarkup][She gives you an ocular pat down, eyeing the length of your figure, and smiles this time a smile that reaches her eyes.][/nomarkup] I suppose if Dominic believes you're worth breaking the Traditions over, then you must be something special?. Good luck with this little test my dear, come back if you succeed? And tell me all about it, I'm so curious!
-> {$pcName}: Thanks.
    {$sforza}: At least you know your manners. That helps. [nomarkup][+1 Kieran Influence][/nomarkup]
    <<ModifyInfluence 1 $kieran>>
    <<jump questionsAboutLastNight>>
-> {$pcName}: How generous of you. [nomarkup][You say with a dose of sarcasm.][/nomarkup]
    {$sforza}: [nomarkup][She laughs, yet her tone is far from amused.][/nomarkup] My dear, you should learn to be more guarded with your intentions if you want to make something of your new life. Not all Kindred are as nice as I am, some think that the solution to a wounded pride is a missing tooth.. [nomarkup][-1 Kieran Influence][/nomarkup]
    <<ModifyInfluence -1 $kieran>>
    {$kieran}: She does have a fiery temperament, but... it's to be expected, given her Blood.
    {$sforza}: Brujah? [nomarkup][She sighs and rolls her eyes.][/nomarkup] I would heartily endorse some anger management classes and, if you would like to live a long prosperous life, to not lean on excuses.
    <<jump questionsAboutLastNight>>
===
title: questionsAboutLastNight
position: 6,20
---
{$pcName}: I came by here last night. Do you remember me? Who I was with?
{$sforza}: I see a lot of faces my dear, this is a popular club! But- I can see that this is serious allow me to recall... [nomarkup][Her lips curl in an amused grin as her eyes drift off to the side, her focus turning towards her own memories.][/nomarkup] Ah, yes I do remember someone mentioning a woman of your description. You're quite famous around these parts, I remember saying that if you were to stop by your first drink would be on me. Thank you for that article on the Golden Lion my dear, it has brought me no end of amusement. I was told you found yourself something of a partner in crime and left before anyone had the opportunity. [nomarkup][She makes a wide, sweeping gesture with her hand, chuckling.][/nomarkup] Maybe he's your Sire?
<<if GetIsPartyMemberRecruited("ANASTASIA") is true>>
{$anastasia}: Damn, and people say [nomarkup]<i>I'm</i>[/nomarkup] messy.
<<endif>>
[nomarkup][Your face flushes red. Apparently, even vampires can be embarrassed. It's mixed with a subtle, unshakeable sense of guilt.][/nomarkup]
-> [nomarkup](Intelligence + Insight) [Why am I feeling this way?][/nomarkup]
<<if Roll("Intelligence","Insight")>=4>>
    [nomarkup][You think you know why... This was the man in your shower. It's the only explanation that makes sense.][/nomarkup] #success
    -> {$pcName}: I think it might be the guy I - found... in my apartment.
    {$kieran}: [nomarkup][He attempts to offer a comforting smile - his eyebrows knit.][/nomarkup] Right. Sorry about that.
    {$sforza}: Making a mess already, piccola mia? But you are a smart one to survive this long. Or just maybe it's those lovely eyes, so sweet. So full of fire - Dominic always did like the one with bite.. Poor influences aside I'm sure, Kieran can help you realize your full potential, and in time, build you into an upstanding member of the Praxis. I anticipate that his mentorship will be an exemplary guiding force, and I would expect nothing less. He is my Childe, after all.
    [nomarkup][Kieran only reciprocates with a shallow nod, though there may be some pride in the way he glances at you momentarily.][+1 Kieran influence][/nomarkup]
    <<ModifyInfluence 1 $kieran>>
<<else>>
    {$kieran}: Maybe, but I think the most sound conclusion is that he's the individual we found in her apartment, based on the evidence we have already collected. #failure
    -> {$pcName}: [nomarkup][What was left of him, anyway. You wince slightly at the memory.][/nomarkup]
    {$sforza}: Making a mess already, piccola mia? Hopefully, Kieran can teach you how to behave. He has such a flair with words and a way with manners. I would hate to hear of his failure to be suitably helpful.
    [nomarkup][Kieran only reciprocates with an anxious, shallow nod.][/nomarkup]
<<endif>>
-> {$pcName}: Did we leave with anybody else?
<<if GetIsPartyMemberRecruited("ANASTASIA") is true>>
    {$anastasia}: [nomarkup][She's a little too eager as she blurts this out.][/nomarkup] A “three's a crowd” situation? Or was a crowd just what you were looking for?
    [nomarkup][Nobody knows how to respond.][/nomarkup]
    {$anastasia}: Why are you looking at me like that? It's just a question. And greatly important to this “investigation”, too. Need to know if there's yet another corpse kicking around, right?
<<endif>>
{$sforza}: My dear if you left with anyone else I can't say.  Perhaps I could try to recall something but then I couldn't give you a clear answer, you know how memories warp things. It can hardly be trusted those hazy memories... and I will admit, I was preoccupied.
-> {$pcName}: (Manipulation + Persuasion) “Preoccupied?”
    <<if Roll("Manipulation","Persuasion")>=3>>
        {$sforza}: [nomarkup][She sighs.][/nomarkup] My dear if you wanted to know about my personal life you are more than welcome to come find me when you are off the job. Until then I will keep my time to myself. #success
        [nomarkup][Out of the corner of your eye, you catch a glimpse of Kieran's brief disapproving frown at that news.][/nomarkup]
        <<jump kateFarewell>>
    <<else>>
        {$sforza}: [nomarkup][Smirks][/nomarkup] A lady shouldn't kiss and tell. #failure
        <<jump kateFarewell>>
    <<endif>>
===
title: kateFarewell
position: 293,-24
---
<<if GetIsPartyMemberRecruited("ANASTASIA") is false and $kateRequest is false>>
{$sforza}: Before you go, I may have a lead for you. See the darling down there harassing my bartender? I would suspect she would know something about this all... [nomarkup][She leans forward as her eyes crinkle with pleasure.][/nomarkup] Diana has something of a type. I do try not to judge just it remains true if I do so or not.
    <<if visited("anastasiaMeeting")==true>>
        <<set $kateRequest to true>>
        [nomarkup][Kate catches her bartenders eyes and raises her glass, indicating you to Anastasia. Even from this distance, you can tell her attempts to woo the woman behind the bar still aren't going well. She's either overly persistent or oblivious, and you're not sure which one's worse.][/nomarkup]
        <<jump kateNudgesTowardAnastasia>>
    <<else>>
        <<set $kateRequest to true>>
        [nomarkup][Kate raises her delicate hand up in a sweeping wave, indicating you to a woman decked out in studded leather garments and steel-toed cleats - a “punk”, if your memory of the lingo serves. Some knowledge of the various subcultures present in the city is useful for a PI, after all. Whatever group she belongs to, there are altogether too many studs affixed to her clothing. Maybe it's supposed to make her look “hard”. Even from this distance, you can tell her attempts to woo the woman behind the bar aren't going well.][/nomarkup]
        <<jump kateNudgesTowardAnastasia>>
    <<endif>>
<<else>>
-> {$pcName}: Thank you for your time.
    {$sforza}: I'd say “anytime”, dear, but that wouldn't be honest, my time is more valuable to me than anything. I do wish you the best in your endeavor, however, It would seem to me a rather horrendous waste of skill for you to be welcomed in only to be shown the door in such short order.
<<endif>>
===
title: kateNudgesTowardAnastasia
position: 308,190
---
{$sforza}: Diana says her name is  Anastasia, a kindred if you can believe it my dear Shown up the past few nights, but hasn't done the courtesy of introducing herself, a shame really she looks like a treat. [nomarkup][Kate's voice drops onto a purr before she gives you a bashful look and covers her mouth with her hand.][/nomarkup] Do excuse, it's just so hard to resist confidence. I've been keeping an eye on her, oh please forestall your assumptions, she hasn't caused any problems, to my knowledge, but please, do let me know if she does? She could use a... tête á téte.
{$kieran}: [nomarkup][Kieran's eyes widen momentarily, taken aback by the statement.][/nomarkup] Since when have you started tolerating poachers in your domain?
{$sforza}: She hasn't poached yet, and she won't if she wants to... continue to put off our inevitable introduction. This I know for a fact. My darling I may not understand the game she thinks she's playing - but I see no reason to stop this delightful little charadé. Eventually, the reason for her sudden appearance and unwavering tendency to linger in my Domain will be laid bare to me. Either she's staking out the place, or she's helplessly in love with my staff and trying desperately to get my attention. [nomarkup][She looks deeply amused and clearly does not believe there is any merit in the latter suggestion.][/nomarkup] Regardless of her motives, if you will allow me to prolong this dance via absenté I'd be grateful.
===
title: subsequentInteractions
---
<<if GetIsPartyMemberRecruited($anastasia) is false>>
    {$sforza}: Back again, piccola mia? I suppose for you I have a few minutes.
<<else>>
    <<if $anastasiaIntroduced is false and GetIsPartyMemberRecruited($anastasia) is true>>
        {$sforza}: Pleasure to finally make your acquaintance, Anastasia. [nomarkup][Anastasia raises an eyebrow, but keeps her bearings.][/nomarkup]
        {$anastasia}: You too, Kate. [nomarkup][She grabs Kate's hand, quickly planting a kiss upon it. Snatching her hand away, Kate grabs Anastasia's hand back forcing Anastasia's fingers against the other woman's soft wrist. She raises those soft fingers up to her lips and presses her lips to the point where Anastasia touched her. Kate's eyes locked on her the whole time.][/nomarkup]
        {$sforza}: No little games for me after all that time hanging yourself off my bar like the days catch? No? I suppose not then Anastasia. Let's speak your language then. Tell me why you've put such efforts into getting my attention, while you still have it.
    <<endif>>
<<endif>>
-> {$pcName}: What can you tell me about Diana?
    <<jump whoIsDiana>>
-> {$pcName}: How did you come to own this place?
    <<jump howSheStarted>>
-> {$pcName}: Tell me about youself. <<if visited("kateTalksAboutHerself") == false>>
    <<jump kateTalksAboutHerself>>
<<if $dianaMet ==true>>
-> {$pcName}: I need to get into Diana's office.
    <<jump getToOffice>>
<<endif>>
-> {$pcName}: I should go.
    {$sforza}: An astute observation, best of luck on your little search.
===
title: whoIsDiana
---
{$sforza}: What an odd question! But I'll entertain it, you've been such a delight all evening, even bringing little Anastasia up for her overdue introduction! Delightful.  Yes, she's my ghoul. She owns this place, the Embassy, and gives it that foreign touch. Running the bar to keep up appearances I find makes the place feel authentic, although she's been away from Britain for... oh dear almost thirty years now! And most importantly for you, my three musketeers, she's mine.  Why do you ask?
-> {$pcName}: You mentioned she was here last night.
    <<jump dianaEveryNight>>
===
title: dianaEveryNight
---
{$sforza}: [nomarkup][She shrugs a little, a little pout as her long lashes brush against her full cheeks shaking her head with a slight sigh.][/nomarkup] Well yes my dear, this is her full-time job and I do try not to hire those who skirt their responsibilities. Unpleasant or not.
-> {$pcName}: [nomarkup][You raise an eyebrow.][/nomarkup] Almost every night?
{$sforza}: I may be old, but I'm not blind. She's been seeing someone, on occasion. But it hasn't interfered with her duties, so I haven't said anything. I like her having her own life. Keeps things interesting.
<<if GetIsPartyMemberRecruited("ANASTASIA") is true>>
    {$anastasia}: [nomarkup][She scoffs, rolling her eyes.][/nomarkup] How generous of you.
    {$sforza}: Anastasia were you hoping that life would include you? I can't imagine there are many reasons to insult my perhaps radical but well-meaning gifts towards a very loyal woman. She works hard and I do try not to begrudge her a dalliance at her whim.
    {$kieran}: [nomarkup][He glares daggers at Anastasia.][/nomarkup]
<<endif>>
<<if $dianaMet ==true>>
    -> {$pcName}: She led me into the alley last night.
    <<jump whatDianaDid>>
<<endif>>
===
title: whatDianaDid
---
{$sforza}: Oh?
-> {$pcName}: She ran to her office before I had the chance.
    {$sforza}: My darling I try very hard to keep track of what happens in my domain but I am not intimately familiar with every alleyway here. Perhaps speak with Diana herself?
    <<set $kateAlleyTalk to true>>
    {$sforza}: [nomarkup][She cocks her head to the side.][/nomarkup] Curious. She probably just had work to do. I wouldn't take it personally.
<<jump subsequentInteractions>>
===
title: howSheStarted
---
<<if GetIsPartyMemberRecruited("ANASTASIA") is true>>
    {$sforza}: Diana owns the place, piccola mia. I just look after it. It was a gift from our ever so “beloved”...
    {$anastasia}: [nomarkup][She whispers.][/nomarkup] I believe her.
    {$sforza}: ... Prince. She saw fit to bestow upon me this modest domain as a token of her gratitude after that nasty business with what happened to the previous Prince. I knew that The Embassy would be the perfect spot to establish a hub where Kindred could gather and socialize, a true haven for our dysfunctional little family. It required a bit of remodeling and adapting to the needs of our kind, but with Diana's help and expertise, it has blossomed.
<<else>>
    {$sforza}: Diana owns the place, piccola mia. I just look after it. It was a gift from our ever so “beloved” Prince. She saw fit to bestow upon me this modest domain as a token of her gratitude after that nasty business with what happened to the previous Prince. I knew that The Embassy would be the perfect spot to establish a hub where Kindred could gather and socialize, a true haven for our dysfunctional little family. It required a bit of remodeling and adapting to the needs of our kind, but with Diana's help and expertise, it has blossomed.
<<endif>>
<<jump additionalQuestions>>
===
title: additionalQuestions
---
    -> {$pcName}: [nomarkup][Might as well ask...][/nomarkup] What do you think about the Prince?
        <<jump katePrinceOpinion>>
    -> {$pcName}: Who is the Prince? And what's the significance of the title? It seems to repeat in nearly all discussions with... our kind.
        <<jump princeExplained>>
    -> {$pcName}: What happened to the previous Prince?
        <<jump previousPrince>>
===
title: katePrinceOpinion
---
[nomarkup][Kieran glares at you.][/nomarkup]
{$sforza}: It is my duty to follow her lead and contribute to the betterment of our community, she has such a strong vision for the future.
-> {$pcName}: That's a non-answer.
    {$sforza}: Is it?
    {$kieran}: We have an investigation to get back to.
    <<jump additionalQuestions>>
===
title: princeExplained
---
{$sforza}: Ah, it's so refreshing to have someone around as oblivious to politics as you. But I will be frank, I do not want to step on the toes of whomever your Sire turns out to be and it's their vision of the future you may be forced to follow. I'd hate to be accused of poaching. Still I would hate to see you thrown out with nothing, Kieran be a dear and educated your new friend on the intricacies of our society? It would be a good refresher for you and it may save her life.
{$kieran}: The Prince is the head figure in charge of the Camarilla's capital-D Domain here in Las Vegas - nearly the entire valley aside from Henderson and Sunset Manor is under our control. I'll inform you of the broader details once our investigation has concluded.
<<jump additionalQuestions>>
===
title: previousPrince
---
{$sforza}: Kieran?
{$kieran}: Benedic was the Prince before Sam. He got himself killed by Sabbat assassins, but they learned their lesson. They've since been hunted down and killed for their crimes against the Praxis.
-> {$pcName}: Sabbat?
    <<jump whatIsSabbat>>
-> {$pcName}: Traditions?
    <<jump whatAreTraditions>>
-> {$pcName}: Camarilla?
    <<jump camarillaExplained>>
-> {pcName}: Final Death?
    <<jump finalDeathExplained>>
===
title: whatIsSabbat
---
    {$kieran}: A bunch of savages.
    {$sforza}: [nomarkup][Kate gestures towards Kieran in approval.][/nomarkup] A dangerous, misguided organization that rejects the Traditions, the very ones that would have prevented you from standing where you are now.. Benedic didn't see them as a serious threat, so often those with bold new ideas are... [nomarkup][She looks over your shoulder with a touch of sadness before that fades as her gaze returns to you][/nomarkup] and thus he suffered Final Death at their hands. Some advise, Desiree, free of change. If I were someone in your position, Heaven forbid, I would do my very best to know as little about them as possible. Nothing good will come for seeking out such things. Ignorance can be a shield.
    <<if GetIsPartyMemberRecruited("ANASTASIA") is true>>
        {$anastasia}: It's just the natural order of things. They weren't skilled enough to avoid the consequences of what they did, so they died. [nomarkup][She shrugs.][/nomarkup]
    <<endif>>
    <<jump previousPrince>>
===
title: whatAreTraditions
---
{$kieran}: A set of six general rules that all of our kind must adhere to to survive. They are also the laws that mister Evans is tasked with enforcing in Las Vegas as Sheriff. Something like the Ten Commandments, if you're religious.
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: More like Hammurabi's Code, if we're talking severity of retribution. Death usually isn't on the table when talking about Christianity.
    {$kieran}: [nomarkup][He sighs, exasperatedly rubbing his forehead.][/nomarkup] This is just semantics, Miss Goth Queen. What I was attempting to get across is their importance as the bedrock which formed the very foundation of the Camarilla as a Kindred sect.
    {$anastasia}: Uh-huh. You can dismiss my points as semantics all you want; I'm just trying to be precise. And I think my example is way more of an accurate comparison than your half-assed suggestion.
    {$kieran}: [nomarkup][His face twists into a pained expression, his eyebrows furrowing.][/nomarkup] ...You have a point. The Bible never does explicitly mention punishments for breaking these guidelines.
    {$anastasia}: [nomarkup][She smirks, looking exceptionally satisfied with herself, before the devilish grin bleeds into a cackle of amusement.][/nomarkup] That looked absolutely excruciating for you to admit, Kiki. That, or you were struggling not to shit yourself.
    {$kieran}: [nomarkup][He blinks several times.][/nomarkup] Kiki? What is that? Or who? [nomarkup][He elects not to acknowledge the other part of that statement. It's probably better that way, too.][/nomarkup]
    {$anastasia}: You.
    {$kieran}: Exactly as I feared. Please cease.
    {$anastasia}: Okay... Kiki.
    {$kieran}: [nomarkup][He places a hand on his forehead and sighs.][/nomarkup]
    {$sforza}: Commanding respect among your peers I see Kieran. I expected nothing less of you.
    {$kieran}: Anastasia... [nomarkup][He looks over to her now with a little more emotion held in his jaw.][/nomarkup]
    {$anastasia}: [nomarkup][She shrugs, grinning ear to ear.][/nomarkup] Hey, I'll maintain it's got a nice ring to it. Really captures your essence.
    {$sforza}: Now I don't mean to rush you but we only have so long until dawn and I distinctly remember someone looking for their absentee parent? I would get to that, I'd hate to be the one returning to Dominic empty-handed.? [nomarkup][She gives Kieran a meaningful look.][/nomarkup]
<<endif>>
<<jump additionalQuestions>>
===
title: camarillaExplained
---
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$kieran}: A Kindred sect founded more than five hundred years ago with the Six Traditions as our core guiding tenets. All of us are members, and...
    {$anastasia}: [nomarkup][He's interrupted by a snort.][/nomarkup]
    {$kieran}: ...citizens, so to speak, of the Praxis. The institution's organization is modeled after feudalistic societies of the middle ages, with the Prince of a given territory being the equivalent to a lord.
<<else>>
    {$kieran}: A Kindred sect founded more than five hundred years ago with the Six Traditions as our core guiding tenets. All of us are members, and citizens, so to speak, of the Praxis. The institution's organization is modeled after feudalistic societies of the middle ages, with the Prince of a given territory being the equivalent to a Lord.
<<endif>>
-> {$pcName}: And the Lady?
{$sforza}: Women's work so rarely intersected with something so crass as leadership you see, so when the systems were being designed such words as Princess were neatly left out. How thoughtful of them, no? [nomarkup][The tone in her voice is sweet but the way she says the words has a distinct air of hatred. Kate smiles at you.][/nomarkup] But it's different now no? The advancement of progress catches us all in it's tide!
<<if Roll ("Wits","Awareness")>=4>>
    [nomarkup][It's a smile but you can feel that may have been bitter, once - sharp and astringent, but those emotions have clearly been dulled by time.][/nomarkup]
<<endif>>
<<jump additionalQuestions>>
===
title: kateTalksAboutHerself
---
{$sforza}: [nomarkup][She just laughs. Kieran looks down at the ground, his eyes affixed to the floor as he shakes his head from side to side.][/nomarkup] My darling you are so sweet but there is the slight chance that your investigation, if left unattended, will end with your neck in dear Dominic's hands. Come back and ask me that when you're not being lead to the guillotine?
<<jump subsequentInteractions>>
===
title: bouncerDifficulties
---
{$sforza}: After you told him I sent you?
-> {$pcName}: Yes ma'am.
    {$sforza}: That won't do at all. He's been so well behaved thus far. You take a seat here dear, I will be just a moment.
    <<set $kateDealsWithBouncer to true>>
    //cut to black - Kate goes from her lodge and takes care of the bouncer//
    {$sforza}: [nomarkup][Returning the woman's swaying hips scatter the light as she walks up to you.][/nomarkup] He won't be an issue anymore.
    {$kieran}: Thank you ma'am.
    -> {$pcName}: What did you do to him?
        {$sforza}: We had a conversation my darling. Do you need anything else?
        {$kieran}: It would be best to refrain from asking prying questions. He's out of our way - that is what counts. Onward, we go.
        <<if GetIsPartyMemberRecruited($anastasia) is true>>
            {$anastasia}: Oooooh how ominous.
            <<jump subsequentInteractions>>
        <<endif>>
===
title: finalDeathExplained
---
{$sforza}: Oh this one is fun. You have already died once, when you were Embraced, and your next death will be the Final one. Be grateful for your second chance, there aren't many of them.
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: Or so they say. There's always room for the spooky and the strange - riiiiight, Kiki?
    {$kieran}: [nomarkup][He sighs. Looks like he's not going to dignify that with a response...][/nomarkup]
    {$sforza}: Kiki? [nomarkup][She looks over at Kieran with an amused smile like a lion looking at a rabbit.][/nomarkup] How adorable my son, it seems to me you're getting along splendidly with all your little friends.
<<endif>>
<<jump previousPrince>>
===
title: getToOffice
---
<<if $kateAlleyTalk == false>>
    {$sforza}: That's very nice Desiree. 
    -> {pcName}: She led me into the alley last night.
    <<set kateAlleyTalk to true>>
    {$sforza}: You should talk to her about that, then.
    -> {$pcName}: She ran off into her office before I had the chance.
    {$sforza}: [nomarkup][She cocks her head to the side.][/nomarkup] With such a courteous and considerate attitude I cannot imagine why. Perhaps she had work to do?
<<else>>
    {$sforza}: [nomarkup][Kate waves her hand without looking at you.][/nomarkup] Speak with the bouncer and tell him his mistress sent you.
    <<if $talkedToBouncer == false>>
        {$kieran}: We appreciate your assistance with this matter.
    <<else>>
        {$pcName}: The bouncer is being difficult.
        <<jump bouncerDifficulties>>
    <<endif>>
<<endif>>
===