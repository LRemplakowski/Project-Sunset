title: alleyArrival
tags:
---
<<set $rollModifier to 0>>
<<declare $missingTooth to false>>
<<declare $numberOfDrinks to 0>>
<<declare $drinkSuccess to false>>
<<declare $wannabeDefector to false>>
<<declare $kateAnastasia to false>>
<<declare $dianaWithKate to false>>
<<declare $dominicHasDiana to false>>
[nomarkup][The alley smells like blood, death, and burnt rubber. A person slumped against the brick wall tilts their head up just enough to make direct eye contact as the door slams closed behind you. Sirens can be heard wailing not too far from here.][/nomarkup] #line:0d12354 
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: Shit! It's the fuzz. Let's get the hell out of here. #line:0694dee 
    {$kieran}: You're welcome to leave, but I cannot until we find her Sire. I'm not going back to Dominic empty-handed. #line:0296d1e 
<<else>>
    -> {$pcName}: Let's get out of here. #line:0604e3f 
        {$kieran}: Not until we find your Sire. I'm not going back to Dominic empty-handed. #line:0f2aadd 
<<endif>>
-> {$pcName}: Aren't you worried about the police? #line:0bbac08 
    {$kieran}: Your Primogen has us covered. #line:0ffe05e 
    -> {$pcName}: Primogen? #line:0e9f5a8 
    <<if GetIsPartyMemberRecruited($anastasia) is true>>
        {$anastasia}: Are we seriously doing this right now? #line:0dfef3d 
    <<endif>>
    {$kieran}: You'll find out soon enough. #line:08c7587 
-> {$pcName}: We need to move. Sooner, rather than later. #line:048181d 
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: Finally, a sensible idea. #line:04dde74 
<<endif>>
//2 ghouls approach//
<<jump ghoulsApproach>>
===
title: ghoulsApproach
---
Ghoul: Got some stragglers, Karl. #line:0d085be 
-> {$pcName}: We just killed all of your friends, in there. Do you really want to try us? (Charisma + Intimidation) #line:09ef5cc 
    <<jump charismaGhoulsRoll>>
-> {$pcName}: [nomarkup][Draw your weapon.][/nomarkup] #line:0549e88 
    <<jump ghoulsCombat>>
===
title: charismaGhoulsRoll
---
<<if Roll("Charisma","Intimidation") >=3>>
    <<jump charismaGhoulsSuccess>>
<<else>>
    <<jump charismaGhoulsFailure>>
<<endif>>
===
title: charismaGhoulsSuccess
---
Karl: You make a good point. Pretend you didn't see us, and we won't say anything? #success #line:0c114ae 
{$kieran}: Deal. #line:0a0c9bd 
//ghouls run away, dialog ends//
===
title: charismaGhoulsFailure
---
Karl: They didn't have you in point-blank range like I do. #line:07a5d51 
//initiate combat//
===
title: ghoulsCombat
---
Karl: Oooo, looks like these ones have some bite. #line:07f7a1e 
Ghoul: Terrible pun, Karl. #line:0d90dd6 
//initiate combat//
===