title: killKidnapper
tags:
position: 490,264
---
<<declare $disciplineUse to false>>
{$kieran}: I vouched for your ability to maintain a low profile, but... You are ever the magnet for trouble, aren't you? #line:0fbaad4 
-> {$pcName}: Do you ever stop complaining? #line:09d7478 
{$kieran}: I assure you, I do. But tonight, our survival takes priority. #line:0e60132 
[nomarkup][The man shuffles to his feet, stumbling.][/nomarkup] #line:048081a 
Drunk Guy: [nomarkup][Slurring.][/nomarkup] Oh my God! You killed them! I'm gonna call the cops! #line:02bd4bd 
{$kieran}: We just saved you from an occurrence any normal person would call a “kidnapping”, risking our own lives along the way. Or are you too inebriated to recognize how close you were to being disappeared? A simple “thank you” would be nice.  #line:037bdef 
{$beast}: The nerd is right, he should be thankful. #line:092d5fe 
Drunk Guy: I'm a pacifist. I have a principled stance against violence of any kind. #line:0d8c706 
{$kieran}: How quaint. But it seems you choose a bad city in which to be one. #line:04cdba9 
<<jump drunkardOptions>>
===
title: drunkardOptions
position: 210,265
---
-> {$pcName}: (Manipulation + Steetwise) What do you think she would have done to you?<<if visited("streetwiseRoll") ==false>> #line:0aaf290 
    <<jump streetwiseRoll>>
-> {$pcName}: (Strength + Intimidate) Do that and you're next.<<if visited("intimidationRoll") ==false>> #line:07e1194 
    <<jump intimidationRoll>>
-> {$kieran}: (Dominate) Let's [nomarkup]<i>forget</i>[/nomarkup] this ever happened.<<if visited("dominateCloudMemory") ==false>> #line:0249559 
    <<jump dominateCloudMemory>>
-> [nomarkup][Kill him.][/nomarkup] #line:0d8716a 
    <<jump killTheDrunkard>>
===
title: streetwiseRoll
position: 40,-74
---
<<if Roll("Manipulation","Streetwise")>=2>>
    Drunk Guy: You guys [nomarkup]<i>did</i>[/nomarkup] save me. I guess I can let it slide. #success #line:0f71b72 
    {$kieran}: We appreciate the gratitude. #line:039313f 
    <<jump drunkardSuccess>>
<<else>>
    Drunk Guy: Still doesn't make it right for you to kill them. That's why we have a criminal justice system. #failure #line:008c641 
    {$innerVoice}: He’s right, but at the same time, horribly naive. #line:0c7ca0e 
    {$kieran}: And where was that system when you were nearly kidnapped? I am sure it was not here. Otherwise, you wouldn’t have needed our help. #line:0c7ede0 
    <<jump drunkardOptions>>
<<endif>>
===
title: intimidationRoll
position: -109,-294
---
<<if Roll("Strenght","Intimidation")>=2>>
    Drunk Guy: I hope you're bluffing, but you did just kill three people in front of me. #success #line:0054aea 
    [nomarkup][You don't even need to say anything, the both of you just stare him down. Ever so slightly puffing your chest.][/nomarkup] #line:0bad890 
    {$beast}: He's afraid. I can smell it. #line:005d467 
    <<jump drunkardSuccess>>
<<else>>
    Drunk Guy: I'm not scared of death. I'd rather die than betray my beliefs. #failure #line:037f79a 
    {$kieran}: Great, a martyr. But for what reason? #line:069892b 
    <<jump drunkardOptions>>
<<endif>>
===
title: dominateCloudMemory
position: 341,-14
---
<<if UseDiscipline("Dominate")>=1>>
    [nomarkup][The man's eyes glaze over as the words leave Kieran's mouth. He goes somewhat limp. Then, a moment later, he suddenly regains his attentiveness, looking around with visible confusion.][/nomarkup] #success #line:0780a65 
    <<set $disciplineUse to true>>
    Drunk Guy: Huh, where am I? #line:0880839 
    {$kieran}: You were just walking home. #line:0d60ff4 
    Drunk Guy: Thanks stranger, you're right. I miss my bed. Mmm, bed. #line:0343eda 
    <<if Roll("Wits","Insight")>=2>>
    [nomarkup][That was an abrupt heel-turn. You cannot shake the feeling that something unnatural happened here - something that Kieran did. Or said. Hard to tell.][/nomarkup] #line:0e0a20b 
    <<jump drunkardSuccess>>
    <<else>>
    <<jump drunkardSuccess>>
    <<endif>>
<<else>>
    Drunk Guy: You're not going to gaslight me into forgetting murder. #failure #line:0356fbb 
    {$kieran}: [nomarkup][He rubs his temple.][/nomarkup] This man's memories are difficult to rifle through, and the alcohol isn't helping. #line:0ace574 
    <<jump drunkardOptions>>
<<endif>>
===
title: killTheDrunkard
position: -328,-137
---
Drunk Guy: Why are you looking at me like that? Stay away! #line:01c9b51 
[nomarkup][The man attempts to fight, but it’s no use. Already in a weakened state, he goes down with a single blow][/nomarkup] #line:0f90220 
{$beast}: Yes, no witnesses. Nice and clean. #line:09ad6ac 
{$kieran}: [nomarkup][Kieran's disappointment turns into barely restrained anger - his frown deep and his nostrils flaring.][/nomarkup] So that's your solution? Reducing people down to obstacles to be removed? It sure is convenient, isn't it? The overwhelming force you've been gifted with. [nomarkup][Kieran Influence -2][/nomarkup]
<<ModifyInfluence -2 $kieran>>
{$kieran}: Just remember, you're not the only one... 
{$kieran}: [nomarkup][He closes his eyes, taking a deep breath in an attempt to calm himself down. All that remains afterward is a glare steeped in bitter resignation.][/nomarkup] Point taken. We need to clean up the scene, if we hope to have a chance of getting out of this mess. Why did you even bother with pretenses to begin with? #line:05e509a 
<<jump kieranComment>>
===
title: drunkardSuccess
position: -140,238
---
[nomarkup][The man limps away into the night.][/nomarkup] #line:02a3e0e 
<<jump kieranComment>>
===
title: kieranComment
position: -509,170
---
<<if $disciplineUse ==true>>
    -> {$pcName}: How did you... I can't even... What kind of Jedi mind trick? #line:0f28bfa 
    {$kieran}: [nomarkup][He raises his brow a little][/nomarkup] Jedi.. what? #line:07706af 
    -> {$pcName}: You told that guy to forget that something happened and he went all... out of place and out of space. #line:0a82cc9 
    {$kieran}: [nomarkup][He seems not to understand at first what you are referring to, but then you see the realization in his eyes.][/nomarkup] Ah, you mean Disciplines. With the powers of my blood, the mind can be dominated and bent to another's will - up to and including memory erasure. #line:09ff9d3 
    -> {$pcName}: Can you teach me that? #line:09b478a 
        {$kieran}: Sadly, no. At least, not so easily. Your blood has other useful, hidden powers, but now is not the time nor place. #line:0e36b59 
        -> {$pcName}: Okay, mister mysterious. #line:0b0172e 
            {$kieran}: We need to get rid of these bodies before anybody comes by. There's a dumpster in the back alley Kate uses for clean up. #line:0035197 
    -> {$pcName}: You say “later” and “not now” an awful lot... #line:0750cf9 
            {$kieran}: That's because I sincerely hope that there will be a “later” for us both. I don't mean to give you a runaround, but it's a necessity given our time constraints. We must remain focused on the task at hand. #line:087b631 
            ->{$pcName}: That's... not reassuring. #line:029eabd 
                {$kieran}: I know. And I don't like it either. You do not have to remind me about it every step of the way. [nomarkup][He pauses, surveying the mayhem surrounding you.][/nomarkup] We need to get rid of these bodies before anybody comes by. There's a dumpster in the back alley Kate uses for clean-up. #line:0401d92 
            ->{$pcName}: Let's say I understand. But to be honest, I don't - not really. #line:0269e5c 
                {$kieran}: I apologize. I will answer your questions, should we make it past this night alive. Right now, however? Finding your Sire takes precedence. [nomarkup][He pauses, surveying the mayhem surrounding you.][/nomarkup] We need to get rid of these bodies before anybody comes by. There's a dumpster in the back alley Kate uses for clean-up. [nomarkup][+1 Kieran influence][/nomarkup] #line:0db6135 
                <<ModifyInfluence 1 $kieran>>
            ->{$pcName}: You said we're in this boat together. And now, you openly admit that you're keeping information from me. Guess how I feel about it. #line:0365609 
                {$kieran}: As if someone you do not entirely trust is holding all the cards. Just know that you're not the only one who is frequently kept out of the loop. [nomarkup][He pauses, surveying the mayhem surrounding you.][/nomarkup] We need to get rid of these bodies before anybody comes by. There's a dumpster in the back alley Kate uses for clean-up. #line:011bbf8 
            ->{$pcName}: “Later for us both”. Are you trying to flirt with me? #line:09cb329 
                {$kieran}: [nomarkup][His gaze is suddenly unfocused, and he blink blink blinks the haziness away.][/nomarkup] ...What? What even gave you the impression that I'm... No, I am not attempting to flirt with you. [nomarkup][He speaks firmly, shaking his head, but it is clear he is more confused than anything else.][/nomarkup] This is neither the place nor time, and definitely not the person, miss Bardot. [nomarkup][He pauses, surveying the mayhem surrounding you.][/nomarkup] We need to get rid of these bodies before anybody comes by. There's a dumpster in the back alley Kate uses for clean-up. [nomarkup][-1 Kieran Influence][/nomarkup] #line:06ab5fc 
                <<ModifyInfluence -1 $kieran>>
<<endif>>
===