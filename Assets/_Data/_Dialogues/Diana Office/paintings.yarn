title: paintings
tags:
---
<<declare $paintingsAreDestroyed to false>>
<<declare $aidanFoster to false>>
<<declare $safeCode to false>>
<<declare $vibrantVistas to false>>
<<declare $safeDiscovered to false>>
<<declare $luciaLoveLetter to false>>
<<declare $paintingsPutDown to false>>
[nomarkup][The wall is decorated with kitsch “post-modern” affronts to art that you just know would somehow do well in the Arts District. Poor investments, for sure, unless tax-evasion is the end goal.][/nomarkup]
-> [nomarkup][Destroy the paintings.][/nomarkup]
    <<jump paintingsDestroyed>>
-> [nomarkup][Examine the paintings closely.][Resolve+Awareness][/nomarkup] <<if visited("paintingAwarenessRoll") == false>>
    <<jump paintingAwarenessRoll>>
-> [nomarkup][Try to dig up some information about art trade from your brain.][/nomarkup]<<if visited("paintingIntelligenceRoll") == false>>
    <<jump paintingIntelligenceRoll>>
-> [nomarkup][Those seem to be completely out of place... you wonder why...][/nomarkup]<<if visited("paintingWitsSuccess") == false>>  
    <<jump paintingWitsRoll>>
<<if $paintingsPutDown ==true>>
    -> [nomarkup][Examine the safe.][/nomarkup]
        <<jump safeExamine>>
<<endif>>
<<if $safeDiscovered ==true>>
    -> [nomarkup][Take down one of the paintings.][/nomarkup] <<if visited("successContinued") == false>>
        <<jump successContinued>>
<<endif>>
-> [Step away.]
===
title: paintingAwarenessRoll
---
<<if Roll("resolve","awareness") >=3>>
    <<jump paintingAwarenessSuccess>>
<<else>>
    <<jump paintingFailure>>
<<endif>>
===
title: paintingIntelligenceRoll
---
<<if Roll("inteligence","finance") >=4>>
    <<jump paintingIntelligenceSuccess>>
<<else>>
    <<jump paintingFailure>>
<<endif>>
===
title: paintingWitsRoll
---
<<if Roll("wits","insight") >=3>>
    <<jump paintingWitsSuccess>>
<<else>>
    <<jump paintingFailure>>
<<endif>>
===
title: paintingAwarenessSuccess
---
[nomarkup][The vibrantly colored soup of mismatched geometric shapes is nauseating, but you manage to see past that. You reach up to dislodge the panels in the polyptych. Youe see that something is hidden behind it, because shadow neat the frame isn't going in straight line. Applying a modest amount of force to the largest painting in the center unlatches it, revealing a safe on the wall. There is also a paper stamp on the back of the canvas.][/nomarkup] #success
===
title: successContinued
---
<<set $safeDiscovered to true>>
-> Continue
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: Jackpot! Now we just have to pry it open.
<<endif>>
{$kieran}: As long as we don't have the right combination prying it open would take entire night... or several.
<<if $paintingsDestroyed ==false>>
    -> [nomarkup][Examine the stamp.][/nomarkup]
    <<jump stampExamine>>
<<endif>>
-> [nomarkup][Examine the safe.][/nomarkup]
    <<jump safeExamine>>
-> [Step away.]
===
title: stampExamine
---
{$kieran}: Did you find something?
-> {$pcName}: I'm not sure. Call it a gut feeling.
    <<if (GetCompanionInfluence($kieran) >4) or ($toldKieranAboutVision ==true)>>
    {$kieran}: I'll leave you to it then. If it's instinct it's probably important. \[He looks at the painting with curiosity\]
        <<if GetIsPartyMemberRecruited($anastasia) is true>>
            {$anastasia}: My, our nerd is giving up to gut feelings. Something happened when I wasn't looking?
        <<endif>>
    <<else>>
    {$kieran}: I hope it's something important, not just "I've got a feeling". We don't have time for art admiration.
    -> Continue
        <<if GetIsPartyMemberRecruited($anastasia) is true>>
            {$anastasia}: It's not even that pretty to look at. \[She cocks her head to the side like she'd like to see the canvas in another direction.\]
        <<endif>>
    <<endif>>
    <<jump paintingSuccessExamination>>
===
title: paintingIntelligenceSuccess
---
[nomarkup][Those are hideous pieces of art, but still - you recognize that this particular set looks like it is worth quite a bit. It had to be bought in one of the local galleries or on an auction. Such pieces often have stamps or notes on their back. It may even contain some useful information. Like when or where this set was bought. You stare at the picture which seems to be made under the acid influence for a minute and then dislodge it from the wall and look at it's back. You are not disappointed - indeed there is a paper stamp... and the safe.][/nomarkup] #success
<<set $safeDiscovered to true>>
<<jump successContinued>>
===
title: paintingWitsSuccess
---
[nomarkup][This painting is here on purpose. It's not a simple wall decoration. It's here for a reason. It just doesn't sit with Diana proudly painting herself as a british punk rocker. Otherwise she'd just place Sex Pistols or The Exploited posters on the wall. Maybe there's something worth looking for behind one part of this poliptich? You go with your gut, and dislodge one of the paintings. You were right. There's a paper stamp on the back. And the safe...][/nomarkup] #success
<<set $safeDiscovered to true>>
<<jump successContinued>>
=== 
title: paintingSuccessExamination
---
<<set $aidanBryant to true>>
<<set $dateCode to true>>
<<set $vibrantVistas to true>>
-> [nomarkup][You squat and squint your eyes trying to read what is written on the piece of paper glued to the canvas.][/nomarkup]
{$kieran}: Vibrant Vistas Gallery... the address... I believe it's somewhere in the Art's District. And the date. 17th June 1969. Probably a date when it was bought.
<<if GetIsPartyMemberRecruited($anastasia) is true and $paintingsAreDestroyed ==true>>
    {$anastasia}: Great! Now we know where we can find more pieces to rip and shred our appreciation for. \[She chuckles.\]
<<endif>>
[nomarkup][Your attention goes to small handwritten note on the stamp - “For my beloved Diana for one of her most important days in her life. Aidan Foster.”][/nomarkup]
-> {$pcName}: Did you heard of any Aidan Foster by any chance?
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: \[Anastasia shooks her head.\] Not that I pay any attention to the guys. \[She shrugged.\]
<<endif>>
[nomarkup][Kieran on the other hand looks like he is thinking.][/nomarkup]
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: Our nerd guy seems to have an idea though.
    {$kieran}: You're right ms Goth Queen. I have.
<<endif>>
{$kieran}: I believe that Aidan Foster was Diana's husband. Rather touchy topic for her. He died in 'nam in 1972 from what I recall. Killed in action. Once I accidentally asked her about it. When I was Kate's ghoul I was helping Diana from time to time with the papers and stumbled upon a mail with the guys name.
-> {$pcName}: Good to know. May be important. Thanks Kieran.
<<if $paintingsAreDestroyed ==false>>
    [nomarkup][You leave the painting alone. You will not gain more information from it.][/nomarkup]
<<endif>>
===
title: paintingFailure
---
[nomarkup][Just a hideous wall decoration, nothing more. You grow more and more frustrated inspecting it. Something stirs inside you with flashing anger and clouding your better judgement.][/nomarkup] #failure
<<if GetBloodPoints($pcName) >1>>
    -> [nomarkup][Try to suppress the feeling.][-1 Willpower][/nomarkup]
        <<DecreaseWillpower ($pcName, 1)>>
        <<jump paintings>>
    -> [nomarkup][Give in to the feeling.][/nomarkup]
        <<jump paintingsDestroyed>>
<<else>>
    -> [nomarkup][You grit your teeth staring at the painting trying to solve this little, annoying mystery in front of your eyes.][/nomarkup]
        <<jump paintings>>
<<endif>>
===
title: paintingsDestroyed
---
[nomarkup][Paintings are't a big obstacle to you. They are just plain canvas hanged on wall. You punch the frame and rip the canvas leaving miserable shreds as a witness to your growing frustration. Somehow you feel better...and terrified. This act was something not entirely in your control.][/nomarkup]
<<set $safeDiscovered to true>>
<<set $paintingsAreDestroyed to true>>
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: Such depths of aggression and viciousness. I like it. You should make your own exhibition one day. \[Anastasia grins\]
<<endif>>
{$kieran}: Why did you do that? Is it how you conduct investigations? \[Kieran influence -1\]
<<ModifyInfluence (-1, $kieran)>>
-> {$pcName}: I... don't know what came over me. <<if visited("beastTalk") == false>>
    <<jump beastTalk>>
-> {$pcName}: Normally no. But I'm pissed. <<if visited("beastTalk") == false>>
    <<jump beastTalk>>
===
title: beastTalk
---
[nomarkup][Kieran frowns, and it isn't this frown of anger or judging, rather of concern][/nomarkup]
{$kieran}: I know what you are talking about. Just try to suppress those impulses. Otherwise we may end up without anything to cling on regarding our investigation.
-> {$pcName}: Is there something you're not telling me?
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: Probably a lot. I guess he tries to hide the facts about the Beast inside of you.
<<endif>>
{$kieran}: Yes, but not because I'm afraid to talk about it or I want to hide anything. It's just a... complicated concept.
<<if (GetCompanionInfluence($kieran) >4)>>
    <<if GetIsPartyMemberRecruited($anastasia) is true>>
        {$kieran}: And she had a lot of them tonight. I just don't want to overburden her. \[did you just heard genuine... concern in his voice?\]
        {$beast}: No, it's not concern, he's just toying with my feelings. He wasn't particularly “concerned” when I had gun next to my head. All he does is complaining.
        [nomarkup][Anastasia laughs at what Kieran said.][/nomarkup]
        {$anastasia}: Complicated? Whats complicated about it? Listen girl, when you hear it just follow your gut. Don't listen to him. He'll just hinder your REAL potential.
        {$beast}: I like that girl. She talks sense.
        {$kieran}: Listen Anastasia, I said to her that I'm going to help her and this is what I do. If she'll just run rampage...
        {$anastasia}: Yes Kieran, I know, your ass is on the line, you repeat it all the time. Not that I care about it.
        <<jump anaAndKieranConflict>>
    <<else>>
        {$kieran}: You had a lot of them tonight. I just don't want to overburden you. \[did you just heard genuine... concern in his voice?\]
        {$beast}: No, it's not concern, he's just toying with my feelings. He wasn't particularly “concerned” when I had gun next to my head. All he does is complaining.
        {$kieran}: Listen, I said to that I'm going to help you and this is what I do. If you'll just run rampage...
        -> {$pcName}: Yes Kieran, I know, your ass is on the line, you repeat it all the time.
    <<endif>>
<<else>>
    <<if GetIsPartyMemberRecruited($anastasia) is true>>
        [nomarkup][Anastasia laughs at what Kieran said.][/nomarkup]
        {$anastasia}: Complicated? Whats complicated about it? Listen girl, when you hear it just follow your gut. Don't listen to him. He'll just hinder your REAL potential.
        {$beast}: I like that girl. She talks sense.
        [nomarkup][Kieran rolls his eyes][/nomarkup]
        {$kieran}: Listen Anastasia, I said to her that I'm going to help her and this is what I do. If she'll just run rampage...
        {$anastasia}: Yes Kieran, I know, your ass is on the line, you repeat it all the time. Not that I care about it.
        <<jump anaAndKieranConflict>>
    <<else>>
        {$kieran}: Listen, I said to that I'm going to help you and this is what I do. If you'll just run rampage...
        -> {$pcName}: Yes Kieran, I know, your ass is on the line, you repeat it all the time.
    <<endif>>
<<endif>>
===
title: anaAndKieranConflict
---
[nomarkup][you see that Kieran will continue and you get a feeling that if you don't stop them both they will just argue till dawn catches you here][/nomarkup]
{$innerVoice}: Shit...
{$beast}: If they won't stop I'm gonna be dead...
-> {$pcName}: Listen guys... BOTH of you. It's my investigation and we'll do as I say alright? It's not only his ass on the line, but mine too.
    [nomarkup][Anastasia and Kieran focus on you, then stare at each other][/nomarkup]
    {$kieran}: We'll end this conversation at another time.
    {$anastasia}: I'll hold you to that. I'm eager to crush all your stupid arguments to dust.
-> {$pcName}: Kieran, do you really want to roll around in her mud till dawn?
    [nomarkup][Kieran looks at you and shooks his head][/nomarkup]
    {$kieran}: You're right. It's not a good place or time for a philosophical discussion. \[+1 Kieran influence\]
    <<ModifyInfluence (+1,$kieran)>>
    [nomarkup][Anastasia frowns at you][/nomarkup]
    {$anastasia}: MY mud? It's him who didn't tell you about the Beast in the first place! \[-1 Anastasia influence\]
    <<ModifyInfluence (-1,$anastasia)>>
-> {$pcName}: Shut up Kieran! For a guy who tells me he's here to help you hide a lot from me!
    {$anastasia}: Yes! Exactly my thought! \[+1 Anastasia Influence\]
    <<ModifyInfluence (+1,$anastasia)>>
    {$kieran}: I'm sorry, okay? There was too little time to tell you everything... \[-1 Kieran influence\]
    <<ModifyInfluence (-1,$kieran)>>
===
title: examineScraps
---
[nomarkup][What is left of paintings is littering the floor. The wood from wrames is shattered. Canvas scraps try desperately to cling onto broken frame. Some of the paint cracked leaving colorul dust on the floor. On one of them you see a ripped, paper stamp.Well... at least someone elses day is far worse than you - you're still in one piece. You cant's say the same about the paintings. Luckily (or not) for you a safe hidden underneath is intact. You didn't even noticed it because of Kieran and Anastasia quarreling about.][/nomarkup]
<<jump successContinued>>
===