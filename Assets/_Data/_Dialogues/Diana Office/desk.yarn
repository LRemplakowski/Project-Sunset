title: desk
tags:
---
[nomarkup][This desk looks like a solid piece of furniture. Surely, anyone who sits behind it is something of a king - or in this case, queen - of the entire place. Its surface isn't neat, though, breaking the illusion of regality. You do not touch the clutter, instead scanning for anything that could tell you why things are so disorganized. A computer, you think, rests on its side.][/nomarkup]
<<if Roll ("Intelligence","Technology")>=4>>
    [nomarkup][Upon closer inspection, it looks to be an IBM model. It's sad that such a technological marvel seems to be so haphazardly disrespected - especially since it's a relatively new model - only made available to consumers a year or two ago.][/nomarkup]\
    <<set $computerModelIdentified to 1>>
<<endif>>
-> [nomarkup][Inspect the desk.][/nomarkup]
    <<jump deskExamination>>
<<if $computerUnusable ==false>>
-> [nomarkup][Boot the computer.][/nomarkup]
    <<jump computerTurnedOn>>
<<endif>>
-> [nomarkup][Check the drawers.][/nomarkup]
    <<jump deskDrawers>>
-> [nomarkup][Leave the desk alone.][/nomarkup]
===
title: deskExamination
---
[nomarkup][There are several items strewn about the desk. A newspaper, an ashtray, several pens and pencils, some documents as well as blank pieces of paper, and some books. None of these are in any way out of the ordinary. The ashtray, though, seems to commemorate something... In the middle, a Union Jack with letters surrounding it is engraved, but you aren't sure what they say without a closer look. That is, you'll have to empty the ashtray of its contents, including an unfinished cigarette still smoldering.][/nomarkup]
-> [nomarkup][Throw the ash away.][/nomarkup] <<if visited("throwAsh") == false>>
    <<jump throwAsh>>
-> [nomarkup][Smoke the remaining cigarette.][/nomarkup] <<if visited("smokeCigarette") == false>>
    <<jump smokeCigarette>>
-> [nomarkup][Inspect the buds.][/nomarkup]
    <<jump inspectBuds>>
-> [nomarkup][Inspect the books.][/nomarkup]
    <<jump deskBook>>
-> [nomarkup][Inspect the newspaper.][/nomarkup]
    <<jump deskNewspaper>>
-> [nomarkup][Leave the items alone.][/nomarkup]
    <<jump desk>>
===
title: throwAsh
---
[nomarkup][The ash caked inside the ashtray is stubborn as a mule. After years of heavy use, the mass of soot does not want to slide easily into the wastebasket... You'll probably need to wash the ashtray in order to see clearly what is engraved on its bottom.][/nomarkup]
-> [nomarkup][Try to read the inscription anyway.](Resolve+Awareness)[/nomarkup]
<<if Roll("Resolve","Awareness") >=3>>
    [nomarkup][You speak slowly, sounding out the letters surrounding the Union Jack you can manage to decipher.][/nomarkup] Spreading Anarchy in Las Vegas since 19... [nomarkup][It's a miracle you were able to read nearly the entire sentence in one go.]/[nomarkup] #success
        {$kieran}: It's probably the year when Diana started. 1969.
        <<set $safeCode to true>>
        [nomarkup][Yout put the ashtray back on the desk.][/nomarkup]
        <<jump deskExamination>>
<<else>>
    [nomarkup][The ashtray silently stares back at you, the stubborn black soot still caked to it. Maybe it's nothing special after all, and you're putting an irrational amount of importance on this?][/nomarkup] #failure
    <<jump deskExamination>>
<<endif>>
-> [nomarkup][Is this ashtray unique?](Intelligence+Investigation)[/nomarkup]
<<if Roll("Intelligence","Investigation") >=3>>
    [nomarkup][You saw these same ashtrays downstairs. You didn't pay too much attention to how they were decorated, though. Maybe it's something similar to casino chips? They often have the year the gambling establishment they belong to opened written on them...][/nomarkup] #success
    -> {$pcName}: When was The Embassy opened?
        {$kieran}: In 1969. Why?
        <<set $safeCode to true>>
        -> {$pcName}: Nothing. Just asking out of curiosity.
            [nomarkup][You put the ashtray back on the desk.][/nomarkup]
            <<jump deskExamination>>
<<else>>
    [nomarkup][The ashtray silently stares back at you, the stubborn black soot still caked to it. Maybe it's nothing special after all, and you're putting an irrational amount of importance on this?][/nomarkup]
    <<jump deskExamination>>
<<endif>>
===
title: smokeCigarette
---
[nomarkup][You pluck the remaining cigarette from the ashtray and take a drag. These are not menthols or some other kind of aromatized tobacco. They're strong though. Definitely a good brand. But you can't recall the name; you gave up the cigarettes fueling your nicotine addiction some time ago, when your financial situation began to really go poorly. Well, at least you've had the pleasure of smoking something like this, now.][/nomarkup]
{$kieran}: I'll bet that Dominic would be able to tell which brand it is just by sniffing the smoke.
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: Won't kill you now. One of the many upsides to being dead.
<<endif>>
<<jump deskExamination>>
===
title: inspectBuds
---
[nomarkup][Typical buds from your typical cigarettes. You can just barely read the brand name - Embassy - written on the edge of the filter. Diana seems to be a big fan of this word. And those cigarettes - nearly every one of them has lipstick stains.][/nomarkup]
<<jump deskExamination>>
===
title: deskBook
---
[nomarkup][Nothing of particular interest. Well, maybe aside from the biography of Anton LaVey with several bookmarks poking out of its pages. When you pick it up, Kieran chuckles. Raising both eyebrows, you stare at him.][/nomarkup]
{$kieran}: I apologize. It's just... I've met the guy in the past. Quite the showman, to be honest. Don't ask. I was on... an investigation of sorts. The point is, I mentioned him once in passing when I was talking with Diana. Didn't think she'd remember him.
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: It sounds like everyone here is a PI. Didn't know it was such a popular pastime.
    {$kieran}: [nomarkup][He shakes his head.][/nomarkup] No, no. It wasn't anything quite so formal - or required, as this is. I was searching... for something.
    {$anastasia}: Still sounds enough like a PI to me. And we [nomarkup]<i>are</i>[/nomarkup] searching for something - or rather someone. A vampire who broke the Camarilla Code of Chastity.
    {$kieran}: It is the Tradition of Progeny. You know this, Miss Goth Queen.
    {$anastasia}: Yup, but I like my version better. Names for things are at least fifty percent more cool when you repeat letters like that. What's the word for that, again?
    {$kieran}: [nomarkup][He sighs wearily.][/nomarkup] Alliteration?
    {$anastasia}: Alliteration, eh? That's a word that describes a very specific thing - so specific that I'll almost never use it, but thanks anyway, Kiki.
    {$kieran}: [nomarkup][He just blinks - slowly. He's not going to take the bait. Nope. Not tonight...][/nomarkup] You're welcome.
<<endif>>
-> [nomarkup][Inspect the bookmarked pages.][/nomarkup]
[nomarkup][One of the bookmarks tells you the date of founding of the Church of Satan - 30th April, 1966.][/nomarkup]
<<set $satanDate to true>>
<<set $satanCode to true>>
{$anastasia}: [nomarkup][She snorts.][/nomarkup] 69 - nice.
{$kieran}: ‘66 actually.
{$anastasia}: You would be a little spoon, wouldn't you, Tinker bell.
<<endif>>
<<jump deskExamination>>
===
title: deskNewspaper
---
[nomarkup][It's Las Vegas's local newspaper - The Daily-Preview. There are several articles about recent local events. One of the advertisements raises awareness for an upcoming event in the Embassy itself.][/nomarkup]
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: Why are you looking at this? It's not like someone is gonna post your obituary.
    [nomarkup][Sadly, she is right. And you'd like it to stay that way. Not only because of the issues your death would undoubtedly cause, but also because it would make your passing real - more so than you are willing or capable of acknowledging at the current moment. Somewhere deep inside, you still hold onto the faint hope that this is just some absurd nightmare, conjured in the wake of yet another drunken stupor...][/nomarkup]
<<endif>>
<<jump deskExamination>>
===
title: computerTurnedOn
---
[nomarkup][As soon as you flip the switch on the side of the box nesting the screen, it flashes to life. A series of scratches and metallic bleeps are emitted from the steel cube. The glass screen shows a series of letters, which disappear less than a second later, the system prompting you for a password.][/nomarkup]
-> [nomarkup][Maybe you don't own one of these machines yourself (and you won't anytime soon), but you know some tricks that may help you here.][Intelligence + Technology][/nomarkup] <<if visited("hackingFailure") == false or visited("hackingSuccess") == false>>
    <<if Roll("intelligence","technology")>=(4-$computerModelIdentified)>>
        <<jump hackingSuccess>>
    <<else>>
        <<jump hackingFailure>>
    <<endif>>
<<if $thelema ==true>>
-> [nomarkup][Enter the password - “Thelema”.][/nomarkup]
        <<jump accessGranted>>
<<endif>>
<<if $satanDate ==true>>
-> [nomarkup][Enter the password - "Satan".][/nomarkup]
        [nomarkup][It was worth a shot. Hope this computer doesn't need an exorcism, now...][/nomarkup]
        <<jump wrongPassword>>
-> [nomarkup][Enter the password - "LaVey".][/nomarkup]
        [nomarkup][It was worth a shot. Diana seems to have gone through his biography with a fine-toothed comb, after all...][/nomarkup]
        <<jump wrongPassword>>
<<endif>>
-> [nomarkup][Enter the password - "Anarchy".][/nomarkup]
        [nomarkup][It was worth a shot, given Diana's general demeanor.][/nomarkup]
        <<jump wrongPassword>>
-> [nomarkup][Enter the password - "Embassy".][/nomarkup]
        [nomarkup][It was worth a shot. This place seems important to her, after all...][/nomarkup]
        <<jump wrongPassword>>
-> [nomarkup][Enter the password - "Password".][/nomarkup]
        [nomarkup][Ah, the old standby. You also try replacing the letters with special characters: from Passw0rd all the way up to the almost-illegible P@$$w0rd. You hope that Diana is <i>so</i> predictable.][/nomarkup]
        <<jump wrongPassword>>
<<if $goldBugPassword ==true>>
    -> [nomarkup][Enter the password - "Gold-Bug".][/nomarkup]
        <<jump wrongPassword>>
<<endif>>
<<if $aidanFoster == true>>
    -> [nomarkup][Enter the password - "Aidan".][/nomarkup]
        [nomarkup][It was worth a shot. A long lost lover immortalized in a computer password might have been too cheesy for her, though.][/nomarkup]
        <<jump wrongPassword>>
<<endif>>
-> [nomarkup][Leave computer as it is.][/nomarkup]
        <<jump desk>>
===
title: hackingSuccess
---
[nomarkup][You don't own one of these machines yourself (and you won't anytime soon), but you know some tricks of the trade that may help you out here. Your fingers brush the keyboard gently as you type. A few commands here and there, and a backdoor is constructed, bypassing the password prompt with ease. The case of your death is justification enough for hacking into Diana's computer. Right?][/nomarkup] #success
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: Show off.
<<endif>>
{$kieran}: I didn't know you knew how to do that. Impressive. Maybe there's something useful on that computer.
<<jump computerFiles>>
===
title: hackingFailure
---
[nomarkup][Stupid machine! Despite your every attempt, the computer refuses to allow you past the login screen - the input cursor's incessant flashing taunting you... Your frustration and anger threaten to boil over into a white-hot rage.][/nomarkup] #failure
<<if GetBloodPoints($pcName) >1>>
    -> [nomarkup][Try to suppress the feeling.][-1 Willpower][/nomarkup]
        <<DecreaseWillpower ($pcName, 1)>>
        <<jump desk>>
    -> [nomarkup][Give in to the feeling.][/nomarkup]
        <<jump computerDestroyed>>
<<else>>
    -> [nomarkup][You grit your teeth, staring daggers at the computer screen. You can't reason with a machine, and a machine has infinite patience.][/nomarkup]
        <<jump desk>>
<<endif>>
===
title: computerDestroyed
---
[nomarkup][You are pissed... Everything in your life tonight is going completely haywire, and this soulless machine is only adding to the distress. Your vision goes red. You don't even remember punching the  screen and breaking the glass - only the pain of lacerated skin and the smell of burnt circuitry has brought you back to your senses.][/nomarkup]
<<DealDamage ($pcName,2,"superficial")>>
<<if Roll ("Intelligence","Science")>=3>>
    [nomarkup][It's a miracle that your outburst didn't cause a power shortage.][/nomarkup]
<<endif>>
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: Haha! Right between the eyes!
<<endif>>
{$kieran}: Goddamnit! There could have been some useful data in there!
-> {$pcName}:  Don't scream at me! I don't even know what happened! <<if visited("beastTalk") == false>>
    <<jump beastTalk>>
===
title: wrongPassword
---
[nomarkup][You write the password in every possible way that got into your head. It looks like it isn't it.][/nomarkup]
<<jump deskExamination>>
===
title: accessGranted
---
[nomarkup][Just when you hit enter, the screen's display changes, bringing you to the welcome page for Diana. Dismissing that window, there are several files on the homepage. Mostly financial stuff dating back to 1969. You guess that she may have had to move the records from paper. Probably a good call? Definitely would have been, for you.][/nomarkup]
-> [nomarkup][Examine recent logs.](Resolve + Investigation)[/nomarkup] <<if visited("recentLogs") == false>>
    <<jump recentLogs>>
-> [nomarkup][Examine older logs.](Intelligence + Finance)[/nomarkup] <<if visited("olderLogs") == false>>
    <<jump olderLogs>>
<<if $safeDiscovered ==true>>
    -> [nomarkup][Search logs for “safe” keyword.][/nomarkup] <<if visited("searchQuerry") == false>>
        <<jump searchQuerry>>
<<endif>>
-> [nomarkup][Leave computer as it is.][/nomarkup]
===
title: recentLogs
---
<<if Roll("Resolve","Investigation")>=4>>
    <<set safeDiscovered to true>>
    [nomarkup][You dig through several monotonously similar sets of data indicating the financial state of the club. As you are nearing admission of defeat, you see an entry indicating that Diana ordered a wall mounted safe. It was quite the expenditure, but as a Vegas club owner, she had the means to afford it. You glance about, scouring the walls for it, but you don't see any indication that it's been installed in plain sight. Maybe it's behind one of the paintings?][/nomarkup] #success
<<else>>
    [nomarkup][You dig through several monotonously similar sets of data indicating the financial state of the club. After a while, though, you decide you've wasted too much time looking at this stuff. You'll find nothing here.][/nomarkup]
<<endif>>
<<jump accessGranted>>
===
title: olderLogs
---
<<if Roll("Inteligence","Finance")>=3>>
    [nomarkup][Nothing too incriminating, here. Just some “creative accounting”, employed  to the benefit of everyone's pocketbooks.][/nomarkup]
        -> [nomarkup][Look for a floppy disc and save the data.][/nomarkup]
            [nomarkup][It may be a small thing, but it could prove quite useful if you'd like to do a bit of blackmail.][/nomarkup]
            <<if GetAttributeSkillPoolSize("Wits","Politics") >=4>>
                [nomarkup][You think... But then again, you probably don't want to use it as leverage against Diana, since she is the “ghoul” of someone who is quite important here. At least not now, when you don't have any backing.][/nomarkup]
                <<jump accessGranted>>
            <<endif>>
<<else>>
    [nomarkup][Nothing of interest. At least to you. You're no accountant, and can't tell if someone has been cooking the books.][/nomarkup] #failure
    <<jump accessGranted>>
<<endif>>
===
title: searchQuerry
---
[nomarkup][The computer sifts through the files in search of anything “safe”-related. A few long moments of waiting, and... bingo! You find that Diana ordered one some time ago, and the file indicates that the code is set to 1969.][/nomarkup]
<<set safeCode to true>>
<<jump accessGranted>>
===
title: deskDrawers
---
[nomarkup][Not much in here. Most of them contain paper, writing utensils, and other miscellaneous items... In one, there was even an unopened pack of Embassy cigarettes. A single drawer, however, remains locked to you, and takes a key. But the lock doesn't look too complicated.][/nomarkup]
<<if $bobbyPin == true>>
-> [nomarkup][Pick the drawer open.][Dexterity + Larceny][/nomarkup]
    <<if Roll("Dexterity","Larceny") >=3>>
        <<jump drawerLockpickSuccess>>
    <<else>>
        <<jump drawerFailure>>
    <<endif>>
-> [nomarkup][Break the lock by force.][Strength + Larceny][/nomarkup]
    <<if Roll("Strength","Larceny") >=4>>
        <<jump drawerStrengthSuccess>>
    <<elseif GetPartyHasDiscipline ("Potence") == true>>
        <<jump drawerStrengthSuccess>>
    <<else>>
        <<jump drawerFailure>>
    <<endif>>
===
title: drawerLockpickSuccess
---
[nomarkup][The lock isn't a challenge for you. All you needed was the bobby pin you found in your bathroom and a little patience.][/nomarkup] #success
<<jump drawerSuccess>>
===
title: drawerStrengthSuccess
---
[nomarkup][The lock isn't strong. It breaks easily when you apply sufficient force while pulling the drawer.][/nomarkup] #success
<<jumo drawerSuccess>>
===
title: drawerFailure
---
[nomarkup][The lock seems to be stronger than you thought. What secrets may it be hiding, if it's as resilient as an armored safe? You'll never know. This is getting more and more exasperating - as if everything around you is sworn to stand in your way...][/nomarkup] #success
<<if GetBloodPoints($pcName) >1>>
    -> [nomarkup][Try to suppress the feeling.][-1 Willpower][/nomarkup]
        <<DecreaseWillpower ($pcName, 1)>>
        <<jump desk>>
    -> [nomarkup][Give in to the feeling.][/nomarkup]
        <<jump drawerDestroyed>>
<<else>>
    -> [nomarkup][This is ridiculous. You count to ten, breathing slowly. And hope nothing of importance is hidden inside the drawer.][/nomarkup]
        <<jump desk>>
<<endif>>
===
title: drawerDestroyed
---
[nomarkup][This is NOT how things are supposed to occur. In your line of work, you are the master of your surroundings. Not the other way around! You yank the drawer towards yourself, unhinging it from its track and launching it across the room. Papers that were stored inside it go flying. Now, there's no way now to make any sort of sense of them - at least not within the precious few hours you have until dawn.][/nomarkup]
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: Hey! At least let me know if you're gonna throw something!
<<endif>>
{$kieran}: By the Blood! That's not how lockpicking works!
-> {$pcName}: Don't scream at me! I don't even know what happened! <<if visited("beastTalk") == false>>
    <<jump beastTalk>>
===
title: drawerSuccess
---
[nomarkup][You pull the drawer open - the lock no longer an obstacle.][/nomarkup]
{$kieran}: Something useful in there?
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: You are thorough, aren't you?
<<endif>>
-> [nomarkup][Sift through the documents.][/nomarkup]
<<if Roll ("Resolve","Investigation") >=4>>
<<set $safeCode to true>>
    [nomarkup][Most of what you find as you search are receipts, placed here for safe-keeping. Eventually, though, you find one about a wall safe, filed along with its manual, as well as a note stating its combination: 1969.][/nomarkup] #success
    <<jump desk>>
<<else>>
    [nomarkup][You see nothing you can use, but the oldest documents here are from 1969.][/nomarkup]
    <<jump desk>>
<<endif>>
===