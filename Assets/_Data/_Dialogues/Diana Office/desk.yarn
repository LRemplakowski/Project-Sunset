title: desk
tags:
---
[nomarkup][This desk looks like a solid piece of furniture. Surely, anyone who sits behind it is something of a king - or in this case, queen, and the club is their castle. Its surface isn’t neat, though, breaking the illusion of regality.][/nomarkup]
[nomarkup][You do not touch the clutter, instead scanning for anything that could tell you why things are so disorganized. A computer, you think, rests on its side.][/nomarkup] #lastline #line:02f5560 
<<if Roll ("Intelligence","Technology")>=4>>
    [nomarkup][Upon closer inspection, it looks to be an IBM model. It's sad that such a technological marvel seems to be so haphazardly disrespected - especially since it's a relatively new model - only made available to consumers a year or two ago.][/nomarkup] #line:04edffd 
    <<set $computerModelIdentified to 1>>
<<endif>>
<<jump deskInteractions>>
===
title: deskInteractions
---

<<if $computerUnusable ==false>>
    -> [nomarkup][Inspect the desk.][/nomarkup] #line:02b7b75 
        <<jump deskExamination>>
    -> [nomarkup][Boot the computer.][/nomarkup] #line:001bf2a 
        <<jump computerTurnedOn>>
    -> [nomarkup][Check the drawers.][/nomarkup] #line:0edbce5 
        <<jump deskDrawers>>
    -> [nomarkup][Leave the desk alone.][/nomarkup] #line:0a6e434
<<else>>
    -> [nomarkup][Inspect the desk.][/nomarkup]
        <<jump deskExamination>>
    -> [nomarkup][Check the drawers.][/nomarkup]
        <<jump deskDrawers>>
    -> [nomarkup][Leave the desk alone.][/nomarkup]
<<endif>>
===
title: deskExamination
---
[nomarkup][There are several items strewn about the desk. A newspaper, an ashtray, several pens and pencils, some documents as well as blank pieces of paper, and some books. None of these are in any way out of the ordinary. The ashtray, though, seems to commemorate something...][/nomarkup]
[nomarkup][In the middle, a Union Jack with letters surrounding it is engraved, but you aren't sure what they say without a closer look. That is, you'll have to empty the ashtray of its contents, including an unfinished cigarette still smoldering.][/nomarkup] #lastline #line:0b35655 
<<jump deskExaminationOptions>>
===
title: deskExaminationOptions
---
-> [nomarkup][Throw the ash away.][/nomarkup] <<if visited("throwAsh") == false>> #line:0d3873e 
    <<jump throwAsh>>
-> [nomarkup][Smoke the remaining cigarette.][/nomarkup] <<if visited("smokeCigarette") == false>> #line:0aba136 
    <<jump smokeCigarette>>
-> [nomarkup][Inspect the buds.][/nomarkup] #line:0288b54 
    <<jump inspectBuds>>
-> [nomarkup][Inspect the books.][/nomarkup] #line:09d398e 
    <<jump deskBook>>
-> [nomarkup][Inspect the newspaper.][/nomarkup] #line:0156f74 
    <<jump deskNewspaper>>
-> [nomarkup][Leave the items alone.][/nomarkup] #line:07bfd15 
    <<jump deskInteractions>>
===
title: throwAsh
---
[nomarkup][The ash caked inside the ashtray is stubborn as a mule. After years of heavy use, the mass of soot does not want to slide easily into the wastebasket... You'll probably need to wash the ashtray in order to see clearly what is engraved on its bottom.][/nomarkup] #line:0d1020c 
-> [nomarkup][Try to read the inscription anyway.] (Resolve+Awareness)[/nomarkup] #line:0e9f52b 
<<if Roll("Resolve","Awareness") >=3>>
    [nomarkup][You speak slowly, sounding out the letters surrounding the Union Jack you can manage to decipher.][/nomarkup] Spreading Anarchy in Las Vegas since 19... [nomarkup][It's a miracle you were able to read nearly the entire sentence in one go.]/[nomarkup] #success #line:0508fcc 
        {$kieran}: It's probably the year when Diana started. 1969. #line:07fa5eb 
        <<set $safeCode to true>>
        [nomarkup][Yout put the ashtray back on the desk.][/nomarkup] #lastline #line:08501c5 
        <<jump deskExaminationOptions>>
<<else>>
    [nomarkup][The ashtray silently stares back at you, the stubborn black soot still caked to it. Maybe it's nothing special after all, and you're putting an irrational amount of importance on this?][/nomarkup] #lastline #failure #line:0b38984 
    <<jump deskExaminationOptions>>
<<endif>>
-> [nomarkup][Is this ashtray unique?] (Intelligence+Investigation)[/nomarkup] #line:0fa0839 
<<if Roll("Intelligence","Investigation") >=3>>
    [nomarkup][You saw these same ashtrays downstairs. You didn't pay too much attention to how they were decorated, though. Maybe it's something similar to casino chips? They often have the year the gambling establishment they belong to opened written on them...][/nomarkup] #success #line:090f77c 
    -> {$pcName}: When was The Embassy opened? #line:03bc814 
        {$kieran}: In 1969. Why do you ask? #line:0867d06 
        <<set $safeCode to true>>
        -> {$pcName}: Nothing. Just asking out of curiosity. #line:0172ed3 
            [nomarkup][You put the ashtray back on the desk.][/nomarkup] #lastline #line:0f7fd6b 
            <<jump deskExaminationOptions>>
<<else>>
    [nomarkup][The ashtray silently stares back at you, the stubborn black soot still caked to it. Maybe it's nothing special after all, and you're putting an irrational amount of importance on this?][/nomarkup] #lastline #line:03f5de0 
    <<jump deskExaminationOptions>>
<<endif>>
===
title: smokeCigarette
---
[nomarkup][You pluck the remaining cigarette from the ashtray and take a drag. These are not menthols or some other kind of aromatized tobacco. They're strong though. Definitely a good brand...][/nomarkup]
[nomarkup][But you can't recall the name; you gave up the cigarettes fueling your nicotine addiction some time ago, when your financial situation began to really go poorly. Well, at least you've had the pleasure of smoking something like this, now.][/nomarkup] #line:09a1746 
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: Won’t kill you now, at least. One of the many upsides to being dead. #line:052ee8e 
<<endif>>
{$kieran}: I'll bet that Dominic would be able to tell which brand it is just by sniffing the smoke. #lastline #line:05ffa43 
<<jump deskExaminationOptions>>
===
title: inspectBuds
---
[nomarkup][Typical buds from your typical cigarettes. You can just barely read the brand name - “Embassy” - written on the edge of the filter. Diana seems to be a big fan of this word. And those cigarettes - nearly every one of them - have lipstick stains.][/nomarkup] #lastline #line:09725ec 
<<jump deskExaminationOptions>>
===
title: deskBook
---
[nomarkup][Nothing of particular interest. Well, maybe aside from the biography of Anton LaVey with several bookmarks poking out of its pages. When you pick it up, Kieran chuckles. Raising both eyebrows, you stare at him.][/nomarkup] #line:07e4f9f 
{$kieran}: I apologize. It’s just... I’ve met the individual in the past. Quite the showman, to be honest. Please don’t ask me to elaborate further. I was on... an investigation, of sorts...
{$kieran}: The point is, I mentioned him once in passing when I was talking with Diana. Didn’t think she would remember him, much less purchase a biography of his life to scrutinize so thoroughly. #line:0adec2b 
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: It sounds like almost everyone here is a PI. Didn’t know it was such a popular pastime. #line:0942ee9 
    {$kieran}: [nomarkup][He shakes his head.][/nomarkup] No, no. It wasn't anything quite so formal - or required, as this is. I was searching... for something. #line:07c06c2 
    {$anastasia}: Vague. But it still sounds pretty similar to  PI business to me. And we [nomarkup]<i>are</i>[/nomarkup] searching for something - or rather someone. A vampire who broke the Camarilla Code of Chastity. #line:0f7102d 
    {$kieran}: It is the Tradition of Progeny. You know this, Miss Goth Queen. #line:0186885 
    {$anastasia}: Yup, but I like my version better. Names for things are at least fifty percent more cool when you repeat the letters at the beginnings of each word  like that. I know there’s a term for it. What is it, again?  #line:08f8e8f 
    {$kieran}: [nomarkup][He sighs wearily.][/nomarkup] Alliteration? #line:0477207 
    {$anastasia}: Alliteration, eh? That’s a mouthful. And a word that describes a very specific thing - so specific that I’ll almost never use it. But thanks anyway, Kiki! #line:0b65972 
    {$kieran}: [nomarkup][He just blinks - slowly. He's not going to take the bait. Nope. Not tonight...][/nomarkup] You're welcome. #lastline #line:0a43699 
<<endif>>
-> [nomarkup][Inspect the bookmarked pages.][/nomarkup] #line:0ee85bf 
[nomarkup][One of the bookmarks tells you the date of founding of the Church of Satan - 30th April, 1966.][/nomarkup] #line:0744d63 
<<set $satanDate to true>>
<<set $satanCode to true>>
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: [nomarkup][She snorts.][/nomarkup] 69 - nice. #line:00c76e1 
    {$kieran}: ‘66 actually. #line:0d3e4fb 
    {$anastasia}: You [nomarkup]<i>would</i>[/nomarkup] be a little spoon, wouldn’t you, Tinker Bell? #line:0d89fd6 
<<endif>>
<<jump deskExaminationOptions>>
===
title: deskNewspaper
---
[nomarkup][It’s the local newspaper of Las Vegas - The Daily Preview. There are several articles about recent local happenings. One of the advertisements even raises awareness for an upcoming event in the Embassy itself.][/nomarkup] #line:0f8ea1b 
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: Why are you looking at this? It's not like someone is gonna post your obituary. #line:07f4e86 
    [nomarkup][Sadly, she’s right. And you’d like it to stay that way. Not only because of the issues your death would undoubtedly cause, but also because it would make your passing real - more so than you are willing or capable of acknowledging at the current moment.][/nomarkup]
    [nomarkup][Somewhere deep inside, you still hold onto the faint hope that this is just some absurd nightmare, conjured in the wake of yet another drunken stupor...][/nomarkup] #line:014343c 
<<endif>>
<<jump deskExaminationOptions>>
===
title: computerTurnedOn
---
[nomarkup][As soon as you flip the switch on the side of the box nesting the screen, it flashes to life. A series of scratches and metallic bleeps are emitted from the steel cube. The glass screen shows a series of letters, which disappear less than a second later, the system prompting you for a password.][/nomarkup] #line:0239e06 
-> [nomarkup][Maybe you don't own one of these machines yourself (and you won't anytime soon), but you know some tricks that may help you here.][Intelligence + Technology][/nomarkup] <<if visited("hackingFailure") == false or visited("hackingSuccess") == false>> #line:07dd03a 
    <<if Roll("intelligence","technology")>=(4-$computerModelIdentified)>>
        <<jump hackingSuccess>>
    <<else>>
        <<jump hackingFailure>>
    <<endif>>
<<if $thelema ==true>>
-> [nomarkup][Enter the password - “Thelema”.][/nomarkup] #line:0e990ce 
        <<jump accessGranted>>
<<endif>>
<<if $satanDate ==true>>
-> [nomarkup][Enter the password - "Satan".][/nomarkup] #line:019d5c0 
        [nomarkup][It was worth a shot. Hope this computer doesn't need an exorcism, now...][/nomarkup] #line:0baf60c 
        <<jump wrongPassword>>
-> [nomarkup][Enter the password - "LaVey".][/nomarkup] #line:03c429b 
        [nomarkup][It was worth a shot. Diana seems to have gone through his biography with a fine-toothed comb, after all...][/nomarkup] #line:07e6146 
        <<jump wrongPassword>>
<<endif>>
-> [nomarkup][Enter the password - "Anarchy".][/nomarkup] #line:0f38d4b 
        [nomarkup][It was worth a shot, given Diana's general demeanor.][/nomarkup] #line:0c862f7 
        <<jump wrongPassword>>
-> [nomarkup][Enter the password - "Embassy".][/nomarkup] #line:0144ca1 
        [nomarkup][It was worth a shot. This place seems important to her, after all...][/nomarkup] #line:009c1be 
        <<jump wrongPassword>>
-> [nomarkup][Enter the password - "Password".][/nomarkup] #line:0ae5f04 
        [nomarkup][Ah, the old standby. You also try replacing the letters with special characters: from Passw0rd all the way up to the almost-illegible P@$$w0rd. You hope that Diana is <i>so</i> predictable.][/nomarkup] #line:00cccee 
        <<jump wrongPassword>>
<<if $goldBugPassword ==true>>
    -> [nomarkup][Enter the password - "Gold-Bug".][/nomarkup] #line:0535c12 
        <<jump wrongPassword>>
<<endif>>
<<if $aidanFoster == true>>
    -> [nomarkup][Enter the password - "Aidan".][/nomarkup] #line:08098b0 
        [nomarkup][It was worth a shot. A long-lost lover immortalized in a computer password might have been too cheesy for her, though.][/nomarkup] #line:0147b49 
        <<jump wrongPassword>>
<<endif>>
-> [nomarkup][Leave computer as it is.][/nomarkup] #line:0aa3bb5 
        <<jump deskInteractions>>
===
title: hackingSuccess
---
[nomarkup][You don't own one of these machines yourself (and you won't anytime soon), but you know some tricks of the trade that may help you out here. Your fingers brush the keyboard gently as you type. A few commands here and there, and a backdoor is constructed, bypassing the password prompt with ease. The case of your death is justification enough for hacking into Diana's computer. Right?][/nomarkup] #success #line:0ef4d63 
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: Show off. #line:081408a 
<<endif>>
{$kieran}: I was not aware you knew how to do that. Impressive. Maybe there’s something useful on that computer. #line:0fff652 
<<jump computerFiles>>
===
title: hackingFailure
---
[nomarkup][Stupid machine! Despite your every attempt, the computer refuses to allow you past the login screen - the input cursor’s incessant flashing taunting you relentlessly... Your frustration and anger threaten to boil over into a white-hot rage.][/nomarkup] #failure #line:0d9f1de 
<<if GetBloodPoints($pcName) >1>>
    -> [nomarkup][Try to suppress the feeling.][-1 Willpower][/nomarkup] #line:0d18ca5 
        <<DecreaseWillpower ($pcName, 1)>>
        <<jump deskInteractions>>
    -> [nomarkup][Give in to the feeling.][/nomarkup] #line:0c1013c 
        <<jump computerDestroyed>>
<<else>>
    -> [nomarkup][You grit your teeth, staring daggers at the computer screen. You can't reason with a machine, and a machine has infinite patience.][/nomarkup] #line:03833e3 
        <<jump deskInteractions>>
<<endif>>
===
title: computerDestroyed
---
[nomarkup][You are pissed... Everything in your life tonight is going completely haywire, and this soulless machine is only adding to the distress. Your vision goes red. You don't even remember punching the  screen and breaking the glass...][/nomarkup]
[nomarkup][Only the pain of lacerated skin and the smell of burnt circuitry has brought you back to your senses.][/nomarkup] #line:0cea852 
<<DealDamage $pcName 2>>
<<if Roll ("Intelligence","Science")>=3>>
    [nomarkup][It's a miracle that your outburst didn't cause a power shortage.][/nomarkup] #line:00f52ea 
<<endif>>
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: Haha! Right between the eyes! #line:00b827a 
<<endif>>
{$kieran}: Goddamnit! There could have been some useful data in there! #line:01caa19 
-> {$pcName}: Don’t yell at me! I don’t even know what happened! <<if visited("beastTalk") == false>> #line:02f5132 
    <<jump beastTalk>>
===
title: wrongPassword
---
[nomarkup][You write the password in every possible way that got into your head. It looks like it isn't it.][/nomarkup] #line:0b2980b 
<<jump deskExaminationOptions>>
===
title: accessGranted
---
[nomarkup][Just after you hit enter, the screen’s display changes, bringing you to the welcome page for Diana. Dismissing that window, there are several files on the homepage. Mostly financial stuff dating back to 1969. You guess that she may have had to move the records from paper. Probably a good call? Definitely would have been, for you.][/nomarkup] #line:03e8efc 
-> [nomarkup][Examine recent logs.] (Resolve + Investigation)[/nomarkup] <<if visited("recentLogs") == false>> #line:0ff1350 
    <<jump recentLogs>>
-> [nomarkup][Examine older logs.] (Intelligence + Finance)[/nomarkup] <<if visited("olderLogs") == false>> #line:0c00f59 
    <<jump olderLogs>>
<<if $safeDiscovered ==true>>
    -> [nomarkup][Search logs for “safe” keyword.][/nomarkup] <<if visited("searchQuerry") == false>> #line:085e30a 
        <<jump searchQuerry>>
<<endif>>
-> [nomarkup][Leave computer as it is.][/nomarkup] #line:03c5c3c 
===
title: recentLogs
---
<<if Roll("Resolve","Investigation")>=4>>
    <<set $safeDiscovered to true>>
    [nomarkup][You dig through several monotonously similar sets of data indicating the financial state of the club. Nearly half an hour is spent on this wearisome slog, and you are nearing admission of defeat, when you spy an entry indicating that Diana ordered a wall mounted safe.][/nomarkup] #success #line:0d51e54 
    [nomarkup][It was quite the expenditure, but as a Vegas club owner, she had the means to afford it. You glance about, scouring the walls for it, but you don't see any indication that it's been installed in plain sight. Maybe it's behind one of the paintings?][/nomarkup] 
<<else>>
    [nomarkup][You dig through several monotonously similar sets of data indicating the financial state of the club. After a while, though, you decide you've wasted too much time looking at this stuff. You'll find nothing here.][/nomarkup] #line:0ad951e 
<<endif>>
<<jump accessGranted>>
===
title: olderLogs
---
<<if Roll("Inteligence","Finance")>=3>>
    [nomarkup][Nothing too incriminating, here. Just some “creative accounting”, employed  to the benefit of everyone's pocketbooks.][/nomarkup] #line:06479de 
        -> [nomarkup][Look for a floppy disc and save the data.][/nomarkup] #line:0f2f61f 
            [nomarkup][It may be a small thing, but it could prove quite useful if you'd like to do a bit of blackmail.][/nomarkup] #line:057ec4d 
            <<if GetAttributeSkillPoolSize("Wits","Politics") >=4>>
                [nomarkup][You think... But then again, you probably don’t want to use it as leverage against Diana, since she’s the “ghoul” of someone quite important to the political ecosystem of Vegas, if your impressions of her are correct.. At least, you don’t want to use it <i>right now,</i> when you don’t have any backing. Best to sit on it for a bit.][/nomarkup] #line:0fda57c 
                <<jump accessGranted>>
            <<endif>>
<<else>>
    [nomarkup][Nothing of interest, at least to you. You’re no accountant, and you can’t tell if someone has been cooking the books.][/nomarkup] #failure #line:00382e1 
    <<jump accessGranted>>
<<endif>>
===
title: searchQuerry
---
[nomarkup][The computer sifts through the files in search of anything “safe”-related. A few long moments of waiting, and... bingo! You find that Diana ordered one some time ago, and the file indicates that the code is set to 1969.][/nomarkup] #line:050058e 
<<set $safeCode to true>>
<<jump accessGranted>>
===
title: deskDrawers
---
[nomarkup][Not much in here. Most of them contain paper, writing utensils, and other miscellaneous items... In one, there is even an unopened pack of Embassy cigarettes. A single drawer, however, remains locked to you, and takes a key. But the lock doesn’t look too complicated...][/nomarkup] #lastline #line:07b41ed 
<<if GetHasItem("IT_Q_BOBBY_PIN") is true>>
-> [nomarkup][Pick the drawer open.][Dexterity + Larceny][/nomarkup] #line:03db049 
    <<if Roll("Dexterity","Larceny") >=3>>
        <<jump drawerLockpickSuccess>>
    <<else>>
        <<jump drawerFailure>>
    <<endif>>
-> [nomarkup][Break the lock by force.][Strength + Larceny][/nomarkup] #line:007fa6d 
    <<if Roll("Strength","Larceny") >=4>>
        <<jump drawerStrengthSuccess>>
    <<elseif GetPartyHasDiscipline ("Potence") == true>>
        <<jump drawerStrengthSuccess>>
    <<else>>
        <<jump drawerFailure>>
    <<endif>>
<<endif>>
===
title: drawerLockpickSuccess
---
[nomarkup][The lock isn't a challenge for you. All you needed was the bobby pin you found in your bathroom and a little patience.][/nomarkup] #success #line:09769ab 
<<jump drawerSuccess>>
===
title: drawerStrengthSuccess
---
[nomarkup][The lock isn't strong. It breaks easily when you apply sufficient force while pulling the drawer.][/nomarkup] #success #line:09b29ad 
<<jumo drawerSuccess>>
===
title: drawerFailure
---
[nomarkup][The lock seems to be stronger than you thought. What secrets may it be hiding, if it's as resilient as an armored safe? You'll never know...][/nomarkup] #failure
[nomarkup][This is getting more and more exasperating - as if everything around you is sworn to stand in your way...][/nomarkup] #line:0f3b4b3 
<<if GetBloodPoints($pcName) >1>>
    -> [nomarkup][Try to suppress the feeling.][-1 Willpower][/nomarkup] #line:0f7fb97 
        <<DecreaseWillpower $pcName 1>>
        <<jump deskInteractions>>
    -> [nomarkup][Give in to the feeling.][/nomarkup] #line:029a945 
        <<jump drawerDestroyed>>
<<else>>
    -> [nomarkup][This is ridiculous. You count to ten, breathing slowly. And hope nothing of importance is hidden inside the drawer.][/nomarkup] #line:0e95273 
        <<jump deskInteractions>>
<<endif>>
===
title: drawerDestroyed
---
[nomarkup][This is NOT how things are supposed to occur. In your line of work, you are the master of your surroundings. Not the other way around! You yank the drawer towards yourself, unhinging it from its track and launching it across the room. Papers that were stored inside it go flying. Now, there's no way now to make any sort of sense of them - at least not within the precious few hours you have until dawn.][/nomarkup] #line:077a990 
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: Hey! At least let me know if you're gonna throw something! #line:07f5833 
<<endif>>
{$kieran}: By the Blood! That's not how lockpicking works! #line:03a425d 
-> {$pcName}: Don't scream at me! I don't even know what happened! <<if visited("beastTalk") == false>> #line:0521161 
    <<jump beastTalk>>
===
title: drawerSuccess
---
[nomarkup][The lock isn’t a challenge for you. All you needed was the bobby pin you found in your bathroom and a little patience.][/nomarkup] #line:0bccefd 
[nomarkup][You pull the drawer open - the lock no longer an obstacle to your snoopery.][/nomarkup]
{$kieran}: Something useful in there? #line:0b15cea 
<<if GetIsPartyMemberRecruited($anastasia) is true>>
    {$anastasia}: You [nomarkup]<i>are</i>[/nomarkup] thorough, aren’t you? [nomarkup][She purrs.][/nomarkup]#line:04b2d6d 
<<endif>>
-> [nomarkup][Sift through the documents.][/nomarkup] #line:0aba321 
<<if Roll ("Resolve","Investigation") >=4>>
<<set $safeCode to true>>
    [nomarkup][Most of what you discover as you search are receipts, placed here for safe-keeping. Eventually, though, you find one about a wall safe, filed along with its manual, as well as a note stating its combination: 1969.][/nomarkup] #success #line:030c021 
    <<jump deskInteractions>>
<<else>>
    [nomarkup][You see nothing you can use, but the oldest documents here are from 1969.][/nomarkup] #line:061b72d 
    <<jump deskInteractions>>
<<endif>>
===