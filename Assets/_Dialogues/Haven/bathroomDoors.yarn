title: bathroomDoors
tags:
---
[nomarkup][The door knob doesn't budge. It's locked. Of course from the inside because the architect must have been a sadist.][/nomarkup] #line:05c4aa7 
<<if ($bathroomDoorInteractedWith == true)>>
<<jump bathroomDoorDialogue>>
<<endif>>
{$innerVoice}: Where did I put that key? #line:059e5bb 
<<CompleteObjective "MAIN_SATURDAY_START" "GRAB_MEDS">>
<<set $bathroomDoorInteractedWith to true>>
<<jump bathroomDoorDialogue>>
===
title: bathroomDoorDialogue
position: 248,2
---
-> {$pcName}: \[Try to force the door open.\] #line:0d96086 
    [nomarkup][Despite your best efforts, the door doesn't budge. The architect definitely was a sadist.][/nomarkup] #line:0877209 
    <<jump bathroomDoorDialogue>>
-> {$pcName}: \[Knock\] #line:031893d 
    {$narrator}: KNOCK. KNOCK. KNOCK. #line:0c48419 
    [nomarkup][No answer.][/nomarkup] #line:0cabaa6 
    {$innerVoice}: Why did I do that? #line:03a66e7 
    <<jump bathroomDoorDialogue>>
-> {$pcName}: \[Walk away.\] #line:069c498 
-> {$pcName}: \[Melee 1\] \[Crowbar the door.\] <<if $hasCrowbar == true>> #line:03c586a 
    <<if Roll("melee") >= 1>>
    [nomarkup][You jimmy the crowbar in between the door and the wall. With a little elbow grease you hear a CREAK, and the door glides open.][/nomarkup] #success #line:0ab6340 
    <<OpenBathroomDoors>>
    <<else>>
    [nomarkup][Perhaps you were a little too eager. The door shatters before you, splinters flying in the air, jamming into your flesh.][/nomarkup] 1 superficial damage. #failure #line:0cccabb 
    {$innerVoice}: Ow! What the fuck!? #line:0c15cec 
    <<DestroyBathroomDoors>>
    <<endif>>
    <<CompleteObjective "MAIN_SATURDAY_START" "ENTER_BATHROOM">>
-> {$pcName}: \[Unlock the door.\] <<if $hasBathroomDoorKey == true>> #line:087f332 
    <<OpenBathroomDoors>>
    <<CompleteObjective "MAIN_SATURDAY_START" "ENTER_BATHROOM">>
===