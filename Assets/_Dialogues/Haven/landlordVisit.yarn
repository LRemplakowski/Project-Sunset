title: landlordVisit
tags:
---
<<PlaySFX "door_knock_light">>
[nomarkup][You rush to open the door. As soon as you do, Kevin the Landlord barges past you and steps into the apartment. He almost immediately covers his nose.][/nomarkup] #line:075fa98 
<<SetDefaultSpeakerPortrait "Kevin">>
Kevin: Gah, what the hell is that smell? #line:021505f 
{$pcName}: What do you mean? #line:0c52f8c 
Kevin: You don't smell that? Smells like… nevermind. Let me take a look at the sink. #line:063cfd0 
-> {$pcName}: Continue. #line:030d4d0 
    <<jump landlordSink>>
===
title: landlordSink
---
<<MoveLandlordAndPCToSinkPositions>>
<<wait 1>>
[nomarkup][Kevin bends over and takes a look at the pipes under the sink. You find yourself looking at him in a different way you can't nail down. You're not attracted to him, but he looks strangely – delicious.][/nomarkup] #line:06da74c 
{$beast}: Feed. #line:0ce6fe3 
[nomarkup][You try your best to suppress your strange hunger.][/nomarkup] #line:04e0d7f 
<<wait 1>>
<<ClearPortraitOverride>>
Kevin: Here we go. #line:0c9d47c 
[nomarkup][Kevin leverages his wrench and loosens a bolt. As the pipe opens, blood sprays him, covering his face and shirt. The Bathroom Key and an eyeball falls onto Kevin's chest. He jumps up with eyes wide in horror.][/nomarkup] #line:099e163 
Kevin: UGH! #line:0447526 
{$beast}: FEED! #line:073ea11 
[nomarkup][Kevin brings his attention to you, clearly in shock. The blood covering him only makes him more appetizing. You catch yourself licking your lips.][/nomarkup] #line:05f98ec 
<<wait 1>>
<<ClearPortraitOverride>>
Kevin: Stay away! Monster! #line:0fc7d02 
[nomarkup][Kevin starts to rush towards the door.][/nomarkup] #line:05624c7 
{$beast}: DON'T DENY ME! #line:0075c99 
<<KillKevin>>
[nomarkup][You pounce on Kevin as a matter of instinct, sinking your teeth into his throat. Wet, warm blood fills your throat. This is what you've been craving.][/nomarkup] #line:02ce3c5 
-> {$pcName}: Continue. #line:0cca2c8 
    <<ClearPortraitOverride>>
    <<jump landlordKilled>>
===
title: landlordKilled
---
[nomarkup][When you retract your bite, his limp body falls to the ground. The shock of what you've just done hits you at once.][/nomarkup] #line:04f2573 
{$innerVoice}: Holy fuck! What did I-? #line:062d910 
<<set $police to true>>
-> {$pcName}: \[Walk away.\] #line:076fb43 
-> {$pcName}: \[Pick up the Bathroom Key.\] #line:0ebb8df 
    <<set $hasBathroomDoorKey to true>>
-> {$pcName}: \[Try to save him.\] \[Medicine 1\] #line:0366f15 
    <<if Roll("medicine") >= 1>>
    [nomarkup][You cover up the wound with a dish rag and administer CPR. Kevin's eyes flutter open. Through exasperated breaths and gurgles you hear him struggle to speak.][/nomarkup] #success #line:01ce0d7 
    Kevin: \[gurgle\] You… \[gurgle\] bitch! \[gurgle\] #line:0ddf7b1 
    [nomarkup][His eyes go blank, and he breathes his last breath. At least he died how he lived.][/nomarkup] #line:041c089 
    {$innerVoice}: Shit! #line:04822b1 
    <<else>>
    [nomarkup][You try to administer CPR, but end up hearing the crack of his ribs breaking. What little of his blood remains pools on the floor.][/nomarkup] #failure #line:00f4d56 
    <<endif>>
    -> {$pcName}: Continue. #line:0eefadf
<<CompleteObjective "FIX_SINK" "EAT_LANDLORD">> 
===