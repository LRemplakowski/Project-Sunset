title: shower
tags:
---
[nomarkup][Ready to relax in a hot shower, you open the shower curtain, only to find a one-eyed corpse half staring back at you. As if this night couldn't get any worse.][/nomarkup] #line:0b235ad 
-> Continue
{$innerVoice}: No! Oh god, please, no! #line:0267002 
-> {$pcName}: \[Intelligence + Medicine\] \[Find the cause of death.\] #line:019d0da 
    <<jump findCause>>
-> {$pcName}: \[Wits + Streetwise\] \[Fleece the body.\] #line:0ff8d9d 
    <<jump searchBody>>
-> {$pcName}: \[Walk away.\] #line:0994e23 
    <<jump afterBodyInteraction>>
===
title: findCause
---
<<if Roll("intelligence","medicine") >= 2>>
[nomarkup][In addition to his missing eye, he has several bruises and gashes across his face and neck. Due to his complexion, he most likely died of blood loss.][/nomarkup] #success #line:0afffb6 
<<else>>
[nomarkup][You're not sure why he died specifically, but the missing eye and hole in his neck certainly didn't help.][/nomarkup] #failure #line:02d0269 
<<endif>>
-> Continue
<<jump afterBodyInteraction>>
===
title: searchBody
---
<<if Roll("wits","streetwise") >= 1>>
[nomarkup][You find his wallet and ID - his name is Diego Vazguez.][/nomarkup] #success #line:06abb8d 
<<AddMoney 10>>
<<else>>
[nomarkup][You find his wallet and ID, but his name is partially smeared in blood. You can only make out his initials - DV.][/nomarkup] #line:0dfae54 
<<endif>>
-> Continue
[nomarkup][You find a black and white photo of him holding a baby.][/nomarkup] #line:0baa1ed 
-> Continue
{$innerVoice}: Poor bastard. #line:084e723 
-> Continue
<<jump afterBodyInteraction>>
===
title: afterBodyInteraction
---
{$innerVoice}: What do I do? #line:0b96ec1 
-> {$innerVoice}: I need to turn myself in. #line:0106b38 
    {$beast}: On the other hand, think about it this way: it won't do anything to help him. He's already dead. #line:0a715e5 
    -> Continue
    <<jump thatVoiceAgain>>
-> {$innerVoice}: I'm not going to jail. #line:02baf0d 
    {$beast}: Not if I can do something about it. #line:07347cc 
    -> Continue
    <<jump thatVoiceAgain>>
===
title: thatVoiceAgain
---
{$innerVoice}: There's that voice again. #line:0994259 
-> Continue
[nomarkup][KNOCK. KNOCK. KNOCK.][/nomarkup]
<<PlaySFX "door_knock_light">>
-> Continue
[nomarkup][It's coming from the front door.][/nomarkup] #line:0154ead 
-> Continue
{$innerVoice}: Who is that? #line:082ed34 
-> Continue
{$beast}: Just ignore it. I don't want more witnesses. #line:00fd9fe 
-> Continue
[nomarkup][BANG! BANG! BANG!][/nomarkup]
<<PlaySFX "door_knock_heavy">>
-> Continue
[nomarkup][They're getting more aggressive.][/nomarkup] #line:07f0684 
<<set $hasTimeToCleanGun to false>>
-> Continue #line:060dd7e 
===