title: shower
tags:
---
[nomarkup][Ready to relax in a hot shower, you open the shower curtain, only to find a one-eyed corpse half staring back at you. As if this night couldn't get any worse.][/nomarkup]
{$innerVoice}: No... No. No. No...! No!
-> {$pcName}: \[Medicine 2\] \[Find the cause of death.\]
    <<jump findCause>>
-> {$pcName}: \[Streetwise 1\] \[Fleece the body.\]
    <<jump searchBody>>
-> {$pcName}: \[Walk away.\]
    <<jump afterBodyInteraction>>
===
title: findCause
---
<<if Roll("medicine") >= 2>>
[nomarkup][In addition to his missing eye, he has several bruises and gashes across his face and neck. He most likely died of blood loss.][/nomarkup] #success
<<else>>
[nomarkup][You're not sure why he died specifically, but the missing eye and hole in his neck certainly didn't help.][/nomarkup] #failure
<<endif>>
<<jump afterBodyInteraction>>
===
title: searchBody
---
<<if Roll("streetwise") >= 1>>
[nomarkup][You find his wallet and ID: his name is Diego Vazguez.][/nomarkup] #success
<<AddMoney 10>>
<<else>>
[nomarkup][You find his wallet and ID, but his name is partially smeared in blood. You can only make out his initials: DV.][/nomarkup]
<<endif>>
[nomarkup][You find a black and white photo of him holding a baby.][/nomarkup]
{$innerVoice}: Poor bastard.
<<jump afterBodyInteraction>>
===
title: afterBodyInteraction
---
{$innerVoice}: What do I do?
-> {$innerVoice}: I need to turn myself in.
    {$beast}: Why? He's just food.
    <<wait 1>>
    <<ClearPortraitOverride>>
    <<jump thatVoiceAgain>>
-> {$innerVoice}: I'm not going to jail.
    {$beast}: Get rid of the body.
    <<wait 1>>
    <<ClearPortraitOverride>>
    <<jump thatVoiceAgain>>
===
title: thatVoiceAgain
---
{$innerVoice}: There's that voice again.
<<PlaySFX "door_knock_light">>
<<wait 1.7>>
[nomarkup][It's coming from the front door.][/nomarkup]
{$innerVoice}: Who is that?
{$beast}: Kill them.
<<PlaySFX "door_knock_heavy">>
<<wait 2>>
[nomarkup][They're getting more aggressive.][/nomarkup]
<<set $hasTimeToCleanGun to false>>
<<DisableInteractionsBeforeDominic>>
-> {$pcName}: Continue.
===