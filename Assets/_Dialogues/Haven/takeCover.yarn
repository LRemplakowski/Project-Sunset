title: takeCover
tags:
position: -17,-494
---
{$pcName}: Oh my God! What if it's the cops? #line:0df94ac 
{$beast}: Who cares? It's them or you. #line:044bd7d 
<<wait 1>>
<<if $kevinMurdered == true>>
{$sheriff}: Ooooh! Looks like someone's been busy. #line:066c8de 
{$kieran}: Come on out, lick! Before you make even more of a mess. #line:0aa174d 
-> {$pcName}: Continue. #line:03904b9 
<<else>>
{$kieran}: Come on out, lick! We know you're here! #line:038aa1c 
<<wait 1>>
{$sheriff}: Hurry up and make this quick. I have plans later. #line:0aecf52 
<<endif>>
<<jump attackIntruders>>
===
title: attackIntruders
position: 307,-420
---
-> {$pcName}: \[Shoot them.\] #line:03075c5 
    <<jump gunAttack>>
-> {$pcName}: \[Crowbar them.\] #line:0d6fdbe 
    <<jump crowbarAttack>>
===
title: crowbarAttack
position: 590,-350
---
<<if Roll("melee") >= 1>>
[nomarkup][Your attack connects, leaving a gnarly gash in Dominic, but he seems unbothered. His flesh reconnects as the wound heals before your eyes.][/nomarkup] #success #line:0d7bb94  
<<else>>
[nomarkup][Barely moving, Dominic nonchalantly dodges your attack with swagger. Unbothered.][/nomarkup] #failure #line:0bb2eda 
<<endif>>
<<jump counterAttack>>
===
title: gunAttack
position: 352,-217
---
<<if Roll("ranged") >= 1>>
[nomarkup][Your attack connects, leaving a gnarly gash in Dominic, but he seems unbothered. His flesh reconnects as the wound heals before your eyes.][/nomarkup] #success #line:05a9fc7  
<<else>>
[nomarkup][Barely moving, Dominic nonchalantly dodges your attack with swagger. Unbothered.][/nomarkup] #failure #line:02a1e47 
<<endif>>
<<jump counterAttack>>
===
title: counterAttack
position: 656,-163
---
{$sheriff}: Nice try, kid. #line:0a48c52 
[nomarkup][He saunters towards you, pinning you to the corner, but he doesn't even raise his hands.][/nomarkup] #line:05603f5 
{$sheriff}: Kieran, search the place for any other “friends” she's made. #line:0137843 
{$kieran}: \[Kieran nods and begins searching the apartment.\] #line:08520a1 
{$sheriff}:  Now do you want to do this the hard way or the easy way? #line:02ee38c 
-> {$pcName}: \[Drop your weapon.\] #line:0950145 
    {$sheriff}: Sharp. #line:0232369 
-> {$pcName}: \[Run.\] #line:0696b65 
    [nomarkup][Before you even take a step, Dominic seemingly reads your mind and moves with inhuman speed to block your exit.][/nomarkup] #line:0e9af4e 
<<jump kieranEndsSearch>>
===
title: kieranEndsSearch
position: 789,-13
---
[nomarkup][The one named Kieran steps out of the bathroom.][/nomarkup] #line:0d68b4e 
{$kieran}: John Doe in here. #line:039f8a1 
<<if $kevinMurdered == true>>
{$sheriff}: Two already. #line:0841b0e 
<<endif>>
[nomarkup][He shakes his head side to side but keeps his devilish smirk.][/nomarkup] #line:0f71bfc 
{$sheriff}: You sure know how to party. I'll give you that. #line:05ad39e 
[nomarkup][He lights a cigar, blowing the smoke in your face.][/nomarkup] #line:0deb44b 
{$sheriff}: You've proven to be quite the headache. I'll be honest with you, I'm gonna kill you, but I'm debating whether I should just go ahead and do it now or drag you off to let my wife decide. #line:02bb6cd 
-> {$pcName}: Continue. #line:0028622 
    <<jump sheriffConfrontation>>
===
title: sheriffConfrontation
position: 811,169
---
{$beast}: Kill him! #line:007e7ff 
[nomarkup][You dig your nails into your flesh to stop the voice in your head. You're gonna have to think clearly to get out of this one.][/nomarkup] #line:0118043 
<<wait 1>>
{$sheriff}: How about this, I bet you have several questions. I'll answer them, so you can at least make peace with your death. Can't have any more ghosts chasing my tail. I'm a taken man. #line:04653f7 
[nomarkup][Several thoughts rush through your head at once. You know you can't take them, and it doesn't look like you can make a run for it either. You don't want to die either.][/nomarkup] #line:051582f 
-> {$innerVoice}: I need to bide my time. #line:046db56 
    <<jump whatsGoingOn>>
===
title: whatsGoingOn
position: 824,309
---
{$beast}: KILL HIM! #line:0d5135c 
<<wait 1>>
[nomarkup][One question in particular sticks out the most.][/nomarkup] #line:098c4bf 
{$pcName}: What's happening to me? #line:0ef36d5 
[nomarkup][Dominic cackles.][/nomarkup] #line:076fafc 
{$kieran}: You don't know? #line:0f4584c 
{$sheriff}: \[He's calmed down but is still chuckling.\] You mean you still haven't figured it out yet? Damn, your sire really did you dirty. Well, no sense in pussyfooting around it, you're a vampire. #line:0bbec3a 
{$innerVoice}: That's… that's impossible. #line:0af388f 
-> {$pcName}: A what? #line:0e3a42f 
    <<jump introductionToSociety>>
===
title: introductionToSociety
position: 616,311
---
{$sheriff}: Don't act like you didn't hear me. #line:0ad2b8f 
{$kieran}: We prefer the term Kindred. #line:0fe3fe6 
{$sheriff}: Technically, yes. If you're a nerd. #line:0b5cb70 
[nomarkup][Kieran squints at Dominic who responds with a shit-eating grin.][/nomarkup] #line:07cf79e 
{$sheriff}: Well, at least now you know you didn't kill for no reason. You were hungry. #line:08722ba 
[nomarkup][The crazy part is that it somewhat makes sense. Not that it makes you feel any better.][/nomarkup] #line:01d43d6 
{$sheriff}: Any other questions? #line:085ca15 
<<jump questionsToSheriff>>
===
title: questionsToSheriff
position: 396,314
---
-> {$pcName}: Why are you going to kill me? <<if visited("whyKillMe") == false>> #line:0902e0b 
    <<jump whyKillMe>>
-> {$pcName}: How did this happen to me? <<if visited("howDidThisHappen") == false>> #line:05409a7 
    <<jump howDidThisHappen>>
-> {$pcName}: What did you mean by sire? <<if visited("whatIsSire") == false>> #line:07ad8b9 
    <<jump whatIsSire>>
-> {$pcName}: Who is my sire? <<if visited("whatIsSire") == true && visited("whoIsSire") == false>> #line:05be31a 
    <<jump whoIsSire>>
-> {$pcName}: Who are you? <<if visited("whoAreYou") == false>> #line:0f77218 
    <<jump whoAreYou>>
-> {$pcName}: And you have a wife? <<if visited("whoAreYou") == true && visited("youHaveWife") == false>> #line:0f5e55d 
    <<jump youHaveWife>>
-> {$pcName}: How did you find me? <<if visited("howYouFindMe") == false>> #line:06e322f 
    <<jump howYouFindMe>>
-> {$pcName}: What happens now? #line:02cd03c 
    <<jump whatHappensNow>>
===
title: whyKillMe
position: 475,142
---
{$sheriff}: Not your fault per say. #line:025fc3f 
<<if $kevinMurdered == true>>
{$kieran}: She killed two people in cold blood. #line:041e8bd 
<<else>>
{$kieran}: She killed a man in cold blood. #line:0ddd67d 
<<endif>>
{$sheriff}: Well there's that, but it's not like we're saints either. Let's just say, we have a way of doing things and unfortunately your blood sucking is gonna fuck up our blood sucking if we don't put a stop to it. No hard feelings. #line:0d01b07 
<<jump questionsToSheriff>>
===
title: howDidThisHappen
position: 37,-20
---
{$kieran}: We call it the Embrace. Your sire drained you of blood and then filled you up with theirs. #line:004bef7 
{$sheriff}: Not as kinky as it sounds. Be thankful you don't remember it. #line:0d4779b 
<<jump questionsToSheriff>>
===
title: whatIsSire
position: 295,39
---
{$sheriff}: Well not to get all birds and the bees on you, but your sire is… Well. #line:0cfd395 
{$kieran}: The person who did this to you. #line:0a9ae96 
{$sheriff}: Your vampire daddy… or mommy. Don't want to assume. #line:0144bcf 
<<jump questionsToSheriff>>
===
title: whoIsSire
position: 21,105
---
{$pcName}: Who is my sire? #line:0d8c617 
{$sheriff}: We were hoping you knew. Wanted to kill you both in one fell swoop.  #line:05db0fb 
{$sheriff}: \[Sighs.\] Things can never be easy.  #line:080f098 
<<jump questionsToSheriff>> 
===
title: whoAreYou
position: -31,211
---
{$sheriff}: Oh my bad. Wouldn't want to be rude. Name's Dominic. I'm the Sheriff, but I'm no pig, just the closest thing we Kindred have to law around here. This is Kieran. He's shy. #line:07ab384 
{$kieran}: I can introduce myself. #line:095e9cb 
{$sheriff}: Sure you can. #line:0b7e88f 
[nomarkup][Kieran grumbles under his breath.][/nomarkup] #line:0a0a8f8 
<<jump questionsToSheriff>>
===
title: youHaveWife
position: -26,347
---
{$sheriff}: \[Laughing\] You sound surprised, but since you asked: my wife, Sam, is the Prince who rules Vegas' vampires. Well the cool one's anyway. What can I say? I like 'em powerful. #line:067372e 
{$sheriff}: \[Sighs\] Pity. She would have liked you. She likes spunk. #line:0ba32d6 
{$kieran}: Ew. #line:0060b1e 
{$sheriff}: You know what I mean. #line:0295fbe 
<<jump questionsToSheriff>>
===
title: howYouFindMe
position: 27,457
---
{$sheriff}: We have our ways. #line:03bb8ba 
{$kieran}: We got an anonymous tip. #line:0ce4f9d 
{$sheriff}: Man, why you gotta give up our ways? #line:07a245e 
[nomarkup][Kieran shrugs.][/nomarkup] #line:0d3b0e4 
<<jump questionsToSheriff>>
===
title: whatHappensNow
position: 410,474
---
{$sheriff}: Well my friend, it looks like our time has come to an end. I'd have loved to introduce you to the Mrs, but unfortunately for you I have plans tonight and am already running late. I hereby sentence you to death 2: Electric Boogaloo. #line:0c17074 
{$kieran}: \[Rolling his eyes\] Is now really the time for jokes? #line:0a5ecb8 
[nomarkup][Kieran walks arond the room, briefly browsing through your things. He stops at the investigation board.][/nomarkup] #line:0725817 
{$sheriff}: I'm trying to lighten the mood, grumpy pants.  #line:0b3de57 
{$sheriff}: \[Sighs\] Let's just get this over with. Promise I'll make it quick. #line:096a78a 
[nomarkup][Dominic draws his .44 Auto Mag pistol. He's a little too enthusiastic as he takes a step towards you.][/nomarkup] #line:0ea70dc 
{$beast}: FIGHT BACK! #line:059e616 
<<wait 2>>
[nomarkup][If there's ever a time for self preservation it's now. You reach for your weapon, but Dominic rushes you and pins you by the throat to the wall. Behind his sunglasses, he's all too chipper for a man holding a gun to your head.][/nomarkup] #line:050f5f8 
[nomarkup][You close your eyes. This is it.][/nomarkup] #line:066e12d 
{$innerVoice}: Chad... #line:06bf482 
-> {$pcName}: Continue. #line:0c93a96 
    <<jump sheIsPI>>
===
title: sheIsPI
position: 712,493
---
{$sheriff}: I want you to know, despite appearances, I'm not into this. #line:035123f 
{$kieran}: Wait a second, Dominic. She's a PI. #line:01681cf 
{$sheriff}: \[Looking back at Kieran, hand still firm around your neck.\] … And? #line:0ebd133 
{$kieran}: Don't you think it'd be useful having a lick indebted to you who knows how to be discreet and sniff out secrets? #line:0aa7997 
-> {$pcName}: Continue. #line:00f2c94 
{$sheriff}: I wouldn't exactly call her discreet. #line:0dd57fa 
{$kieran}: Cops haven't got her yet. #line:0549150 
[nomarkup][Dominic slightly loosens his grip, but remains steadfast. His brow furrows. You can see the cogs in his mind spin.][/nomarkup] #line:0a17fa0 
<<jump keepHerAlive>>
===
title: keepHerAlive
---
{$sheriff}: I see why Kate keeps you around, Kieran. You make good points, but Sam will be pissed if she found out I let a fledgling who can't even change their own diaper go. #line:0a43186 
-> {$innerVoice}: I'm not a child, dickwad. #line:02971be 
{$kieran}: Keep her on a short leash for the time being. A trial run and then bring her to the Court once she's proven herself useful. If she's not, proceed as planned. #line:09c533e 
[nomarkup][Dominic stays silent for a few moments before his cheeky smile returns. He lets go of your throat as quickly as he grabbed it, causing you to drop to the floor. He turns around to face Kieran.][/nomarkup] #line:00b1691 
{$sheriff}: You've convinced me, but she's your responsibility. She fucks up. Your ass is on the line too. Hope whatever ulterior motive you have is worth it. #line:0fa4440 
{$kieran}: ... #line:09c2117 
{$kieran}: ... Okay. #line:0da2fc0 
-> {$pcName}: Thanks, I guess. #line:0f4abec 
    {$sheriff}: That's the spirit. You should be thanking Kieran. #line:03d3f41 
-> {$pcName}: I'm nobody's pet. #line:0efda3e 
    {$sheriff}: Whatever you want to call it. Better behave though less you'll be put down. You should be thanking Kieran. #line:044d270 
{$sheriff}: I do have one condition though because I can't go home to my wife breaking too many Traditions less the rabble starts thinking I'm handing out hall passes willy-nilly. You need to find your sire. Tonight. Or else. #line:0f11782 
{$pcName}: That's impossible! I don't even remember where I was last night. #line:057aca1 
[nomarkup][Kieran curses under his breath.][/nomarkup] #line:0c7c45f 
{$sheriff}: Well hate to be an asshole, but that sounds like a “you” problem. Just kidding, I love being an asshole. #line:00a2599 
[nomarkup][He taps his foot a couple times, glancing at the ground.][/nomarkup] #line:00eaa92 
{$sheriff}: I can give you a clue however. #line:05ed23f 
-> {$pcName}: Continue. #line:0621f83 
    <<jump clueRitual>>
===
title: clueRitual
---
[nomarkup][In a flash, he pulls out a knife and cuts your palm open. Thick, dark blood slowly leaks out.][/nomarkup] #line:01bf53e 
{$pcName}: Ow! #line:013070c 
{$sheriff}: \[He grips your hand and slurps up a glob of blood. As his tongue makes contact with the slash, it seals before you.\] #line:029daf2 
-> {$innerVoice}: Can I do that? #line:07ee001 
{$sheriff}: Ah, of course you're a Brujah. #line:00cbb03 
-> {$pcName}: What's that supposed to mean? #line:01a0f6b 
{$sheriff}: You come from an ancient bloodline of Kindred known for their- “radical” tendencies with roots as far back as the Bolshevik Revolution and ancient Carthage.  #line:00ebf01 
{$sheriff}: Basically, it's not your fault you're annoying. Also based on the potency of your blood, I can tell your sire is not somebody to be messed with, so best of luck you two.  #line:0ccaf34 
[nomarkup][Dominic starts walking toward the doors.][/nomarkup] #line:082d650 
{$sheriff}: I'm off. I'll send a team to clean up later. Pleasure to finally meet you Desiree. I'll check in later. \[He takes off his shades and winks.\] #line:08a2728 
-> {$pcName}: Continue. #line:07b65fb 
    <<jump figureOutWhereYouWere>>
===
title: figureOutWhereYouWere
---
{$kieran}: Well, no time to waste. Do you remember anything from yesterday? #line:00fb93f 
-> {$pcName}: I wish I did. #line:0f6de8a 
    {$kieran}: Well that's helpful. #line:04c07c4 
-> {$pcName}: I woke up. #line:0e7276a 
    {$kieran}: Smart ass. Great! #line:02ccffd 
{$kieran}: Let's look around for any clue. If you're going down, I'm not going down with you. #line:0d5f2d7 
-> {$pcName}: Thanks for saving me. #line:0aa3f25 
    [nomarkup][Kieran shrugs.][/nomarkup] #line:0251d36 
    {$kieran}: I didn't do it for you. #line:09ad626 
-> {$pcName}: So you're my jailor. #line:07813d7 
    [nomarkup][Kieran shrugs.][/nomarkup] #line:017bd3f 
    {$kieran}: More like your cellmate. #line:0bc77c9 
{$pcName}: Why did you do it then? #line:0f5c4f0 
{$kieran}: We don't have time to chit-chat. #line:01ca6d4 
-> {$pcName}: Continue. #line:0611bb9 
===