title: bathroomDoors
tags:
---
Narrator: \[The door knob doesn't budge. It's locked. Of course from the inside because the architect must have been a sadist.\]
<<if ($bathroomDoorInteractedWith == true)>>
<<jump bathroomDoorDialogue>>
<<endif>>
Desiree's Inner Monologue: Where did I put that key?
<<CompleteObjective "05d2a735-8b59-48bb-904a-b11e8f5d8dc5" "GRAB_MEDS">>
<<set $bathroomDoorInteractedWith to true>>
<<jump bathroomDoorDialogue>>
===
title: bathroomDoorDialogue
position: 248,2
---
-> {$pcName}: \[Try to force the door open.\]
    Narrator: \[Despite your best efforts, the door doesn't budge. The architect definitely was a sadist.\]
    <<jump bathroomDoorDialogue>>
-> {$pcName}: \[Knock\]
    Narrator: KNOCK. KNOCK. KNOCK.
    Narrator: \[No answer.\]
    Desiree's Inner Monologue: Why did I do that?
    <<jump bathroomDoorDialogue>>
-> {$pcName}: \[Walk away.\]
-> {$pcName}: \[Melee 1\] \[Crowbar the door.\] <<if $hasCrowbar == true>>
    <<if Roll("melee") >= 1>>
    Narrator: \[You jimmy the crowbar in between the door and the wall. With a little elbow grease you hear a CREAK, and the door glides open.\] #success
    <<OpenBathroomDoors>>
    <<else>>
    Narrator: \[Perhaps you were a little too eager. The door shatters before you, splinters flying in the air, jamming into your flesh.\] 1 superficial damage. #failure
    Desiree's Inner Monologue: Ow! What the fuck!?
    <<DestroyBathroomDoors>>
    <<endif>>
    <<CompleteObjective "05d2a735-8b59-48bb-904a-b11e8f5d8dc5" "ENTER_BATHROOM">>
-> {$pcName}: \[Unlock the door.\] <<if $hasBathroomDoorKey == true>>
    <<OpenBathroomDoors>>
    <<CompleteObjective "05d2a735-8b59-48bb-904a-b11e8f5d8dc5" "ENTER_BATHROOM">>
===