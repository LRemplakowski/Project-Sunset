title: sink
tags:
---
<<if $vomited == true>>
Narrator: \[You forgot the sink was already clogged and now chunks of red liquid float in the old, soapy dish water. A part of you worries it's blood, but it's not like you can afford a visit to the ER, so there's no use in worrying about it.\]
Desiree's Inner Monologue: Probably just wine.
<<else>>
Narrator: \[The sink is clogged. Again.\]
<<endif>>
-> {$pcName}: \[Intelligence 2\] \[Repair it yourself.\]
    <<jump sinkRepair>>
-> {$pcName}: I better call the landlord. \[Walk away.\]
===
title: sinkRepair
---
<<if Roll("Intelligence") >= 2>>
Narrator: \[Under the sink, as you open up the U-Pipe, cold blood squirts you in the face.\] #success
<<else>>
Narrator: \[The pipe breaks off into your hand, destroying the sink in the process. What is undeniably backed-up blood and whatever else was clogging the sink sprays you leaving you soaked.\] #failure
<<set $bloodsoaked to true>>
<<endif>>
Narrator: \[You find the source of the clog, the Bathroom Key and an eyeball.\]
<<set $hasBathroomDoorKey to true>>
<<set $police to true>>
<<CompleteObjective "57d83620-89af-4716-aa65-6c5b321e07fe" "bdf1b11a-9d9a-4f93-8889-4e95728d2ad1">>
Desiree's Inner Monologue: What the fuck?
-> {$pcName}: \[Throw the eye back in the sink.\]
    Narrator: \[A wet PLOP as it flops into the bloodstained sink.\]
    <<jump needShower>>
-> {$pcName}: \[Eat the eye.\]
    Narrator: \[Before you can stop yourself, you fit the whole eyeball into your mouth. Chewing on it: a slimy, viscous fluid squirts in between your teeth. It's grotesque but oddly satisfying.\]
    <<ModifyHunger {$pcName} -1>>
    Narrator: \[You stare at your now empty, bloodstained palm. A part of you feels like bursting into tears.\]
    Desiree's Inner Monologue: ...
    Desiree's Inner Monologue: What is happening to me?
    Desiree's Inner Monologue: ...
    <<jump needShower>>
===
title: needShower
---
{$pcName}: I need a shower.
-> {$pcName}: Continue.
===